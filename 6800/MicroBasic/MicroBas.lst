AS02 Assembler for M6802 [1.42].                                     Page    1
-------------------------------- MicroBas.asm --------------------------------

2128 lines read, no errors in pass 1.
                        		list
                        ;-----------------------------------------------------
                        ; MICRO BASIC PLUS SOURCE LISTING
                        ;
                        ; MICRO BASIC PLUS
                        ; COPYRIGHT (C) 1976 BY
                        ;
                        ; TECHNICAL SYSTEMS CONSULTANTS
                        ; BOX 2574
                        ; W. LAFAYETTE INDIANA 47906
                        ;
                        ;
                        ; Additions made by Bob Applegate, K2UT, bob@corshamte
                        ; www.corshamtech.com
                        ;
                        ; Changes include:
                        ;
                        ; * Modified for assembling under AS02 assembler
                        ;   http://www.kingswood-consulting.co.uk/assemblers/i
                        ;
                        ; * Added TSC copyright message at start-up.  They des
                        ;   credit and it also identifies which BASIC is in us
                        ;   also impressive at demos when people see the 1976
                        ;   copyright :-)
                        ;
                        ; * General clean-up of the code.  No changes, just co
                        ;   spaces to tabs, etc.
                        ;
                        ; * Added an assembly option so the INTBRK function ca
                        ;   built for either MP-C or MP-S (ACIA) card.
                        ;
                        ; * Fixed the bug in PUTLBL where the second byte was 
                        ;   picked up.
                        ;
                        ; In general, the code is exactly as it was.  I have a
                        ; a few comments here and there.  There are also some
                        ; opt/noopt lines that were needed so the as02 assembl
                        ; would not optimize some code sections.  When trying 
                        ; get the code to build I wanted to be sure everything
                        ; EXACTLY the same as the original, but the optimizati
                        ; make the code a bit quicker.
                        ;
0000 =                  FALSE		equ	0
ffff =                  TRUE		equ	~FALSE
                        ;
                        ; ASCII control codes
                        ;
0003 =                  ETX		equ	$03	;CTRL-C
0004 =                  EOT		equ	$04	;CTRL-D
0007 =                  BEL		equ	$07	;CTRL-G
0008 =                  BS		equ	$08	;CTRL-H
000a =                  LF		equ	$0a	;CTRL-L
000d =                  CR		equ	$0d	;CTRL-M
0018 =                  CAN		equ	$18	;CTRL-X
                        ;
                        ;-----------------------------------------------------
                        ; START OF USER CONFIGURABLE ITEMS
                        ;
                        ; RAMBASE is where variables are stored and must be RA
                        ;
0000 =                  RAMBASE		equ	$0000	;variables
AS02 Assembler for M6802 [1.42].                                     Page    2
-------------------------------- MicroBas.asm --------------------------------

                        ;
                        ; BASICBASE is where the interpreter goes, and can be
                        ; either RAM or ROM.
                        ;
0100 =                  BASICBASE	equ	$0100	;where interpreter resides
                        ;
                        ; CODEBASE is where the user's BASIC programs are plac
                        ; and must be in RAM.
                        ;
1000 =                  CODEBASE	equ	$1000	;where user programs start
                        ;
                        ; The last address available for use by the user's
                        ; BASIC program.  This depends on the amount of RAM in
                        ; your system, so choose wisely.  Also, the EXTERN
                        ; space is located just past this.
                        ;
1eff =                  RAM_END		equ	$1EFF	;8K = 1EFF, 16K = 3EFF, etc
                        ;
                        ; If true, use the ACIA (6850) instead of the PIA.  Se
                        ; for MP-S or other boards using an ACIA.
                        ;
ffff =                  USE_ACIA	equ	TRUE
                        ;
                        ; If USE_ACIA is TRUE, this must be set to indicate th
                        ; base address of the ACIA.
                        ;
8004 =                  ACIA_BASE	equ	$8004	;8004 is slot 1
                        ;
                        ; If true, print out copyright message at start-up.
                        ; The original did not print a copyright but it adds a
                        ; nice bit of class.
                        ;
ffff =                  SHOW_COPYRIGHT	equ	TRUE
                        ;
                        ; If this is true, then force the USR function to have
                        ; an RTS.  If FALSE then the code just assembles on it
                        ; Leave FALSE by default; that's the way the original
                        ; code worked.
                        ;
0000 =                  FORCE_RTS	equ	FALSE
                        ;
                        ; END OF USER CONFIGURABLE ITEMS
                        ;-----------------------------------------------------
                        ;
                        ; EQUATES
                        ;
a07f =                  STACK		equ	$A07F
                        	if USE_ACIA
8004 =                  ACIASTAT	equ	ACIA_BASE	;status register
8005 =                  ACIADATA	equ	ACIA_BASE+1	;data register
                        	else
                        PIAADR		equ	$8004
                        	endif
a002 =                  PFILBG		equ	$A002
a004 =                  PFILEN		equ	$A004
1f00 =                  EXTERN		equ	RAM_END+1
e0e3 =                  MONITR		equ	$E0E3
a048 =                  MONPC		equ	$A048
a000 =                  STKBOT		equ	$A000
                        		page
AS02 Assembler for M6802 [1.42].                                     Page    3
-------------------------------- MicroBas.asm --------------------------------

                        ;
                        ; TEMPORARY STORAGE
                        ;
                        		bss
0000 =                  		org	RAMBASE
0000 =                  RNDM		rmb	4
0004 =                  BUFPNT		rmb	2
0006 =                  FORSTK		rmb	2
0008 =                  DIMPNT		rmb	2
000a =                  XTEMP3		rmb	2
000c =                  DATAST		rmb	2
000e =                  DATAPT		rmb	2
0010 =                  TRYVAL		rmb	2
0012 =                  CRFLAG		rmb	1
0013 =                  QMFLAG		rmb	1
0014 =                  ROWVAR		rmb	1
0015 =                  ROWCON		rmb	1
0016 =                  COLCON		rmb	1
0017 =                  TABFLG		rmb	1
0018 =                  DIMFLG		rmb	1
0019 =                  RUNFLG		rmb	1
001a =                  DATAFL		rmb	1
001b =                  SUBCNT		rmb	1
001c =                  LETFLG		rmb	1
001d =                  FLDCNT		rmb	1
001e =                  NXPNTR		rmb	2
0020 =                  XTEMP		rmb	2
0022 =                  XSAVE		rmb	2
0024 =                  XSAVE2		rmb	2
0026 =                  NUMCNT		rmb	1
0027 =                  NEGFLG		rmb	1
0028 =                  NOEXFL		rmb	1
0029 =                  EXTRA		rmb	2
002b =                  COUNT		rmb	1
002c =                  STKCNT		rmb	1
002d =                  AUXCNT		rmb	1
002e =                  SIGN		rmb	1
002f =                  AXSIGN		rmb	1
0030 =                  OVFLBF		rmb	1
0031 =                  XTEMP2		rmb	2
0033 =                  XTEMP4		rmb	2
0035 =                  XTEMP5		rmb	2
0037 =                  CPX1		rmb	2
0039 =                  CPX2		rmb	2
003b =                  STKEND		rmb	3
003e =                  CHRCNT		rmb	1
003f =                  OPSTAK		rmb	32
005f =                  AC		rmb	3
0062 =                  NUMBER		rmb	3
0065 =                  AX		rmb	3
0068 =                  BUFFER		rmb	72
                        
                        * LABEL TABLE
                        
00b0 =                  LBLTBL		rmb	78
00fe =                  STKTOP		rmb	2
                        
                        * CONSTANTS
                        ;
                        ; BACKSP is what key is recognized as erasing the
                        ; previous key on input.
                        ;
AS02 Assembler for M6802 [1.42].                                     Page    4
-------------------------------- MicroBas.asm --------------------------------

0008 =                  BACKSP		equ	BS	;originally BS $08
                        ;
                        ; DELCOD is the key that deletes the entire line.
                        ;
0018 =                  DELCOD		equ	$18	;originally CAN $18
                        ;
                        ; PRMPTC is the prompt character.  The original code
                        ; had an exclamation point but you can change it to
                        ; whatever you want.
                        ;
0021 =                  PRMPTC		equ	'!'	;originally '!'
                        		page
AS02 Assembler for M6802 [1.42].                                     Page    5
-------------------------------- MicroBas.asm --------------------------------

                        		code
0100 =                  		org	BASICBASE
                        
                        * MAIN PROGRAM
                        
0100 : 7e01f3           START		jmp	MICBAS	;JMP TO BEGIN
0103 : 7e0203           RESTRT		jmp	FILBUF
                        
                        * EXTERNAL I-O ROUTINES
                        
0106 : 7ee1d1           OUTEEE		jmp	$E1D1
0109 : bde1ac           INCH		jsr	$E1AC
010c : 7e04a4           BREAK		jmp	INTBRK
010f : 1eff             MEMEND		fdb	RAM_END
                        
                        * KEYWORD AND JUMP TABLE
                        
0111 : 505249           KEYTBL		fcc	"PRI"
0114 : 04fa             		fdb	PRINT
                        
0116 : 494e50           		fcc	"INP"
0119 : 07e9             		fdb	INPUT
                        
011b : 494620           		fcc	"IF "
011e : 0903             		fdb	IFS
                        
0120 : 4c4554           		fcc	"LET"
0123 : 07c5             LETADR		fdb	LET
                        
0125 : 464f52           		fcc	"FOR"
0128 : 09c7             		fdb	FOR
                        
012a : 4e4558           		fcc	"NEX"
012d : 09ee             		fdb	NEXT
                        
012f : 474f54           		fcc	"GOT"
0132 : 07d3             		fdb	GOTO
                        
0134 : 474f53           		fcc	"GOS"
0137 : 097c             		fdb	GOSUB
                        
0139 : 4f4e20           		fcc	"ON "
013c : 08c7             		fdb	ONGOTO
                        
013e : 524554           		fcc	"RET"
0141 : 09a4             		fdb	RETURN
                        
0143 : 524541           		fcc	"REA"
0146 : 0877             		fdb	READ
                        
0148 : 444154           		fcc	"DAT"
014b : 0868             		fdb	DATA
                        
014d : 524553           		fcc	"RES"
0150 : 08bd             		fdb	RESTOR
                        
0152 : 44494d           		fcc	"DIM"
0155 : 06c4             		fdb	DIM
                        
0157 : 455854           		fcc	"EXT"
015a : 0754             		fdb	EXTRNL
                        
AS02 Assembler for M6802 [1.42].                                     Page    6
-------------------------------- MicroBas.asm --------------------------------

015c : 4d4f4e           		fcc	"MON"
015f : e0e3             		fdb	MONITR
                        
0161 : 454e44           		fcc	"END"
0164 : 0203             		fdb	FILBUF
                        
0166 : 52454d           		fcc	"REM"
0169 : 0757             		fdb	RUNEXC
                        
016b : 52554e           		fcc	"RUN"
016e : 07b2             		fdb	RUN
                        
0170 : 4c4953           		fcc	"LIS"
0173 : 043f             		fdb	LIST
                        
0175 : 534352           		fcc	"SCR"
0178 : 01f3             		fdb	MICBAS
017a : 00               		fcb	0
                        
017b : 524e44           FCTTBL		fcc	"RND"
017e : 0b11             		fdb	EVAL88
                        
0180 : 414253           		fcc	"ABS"
0183 : 0b0d             		fdb	EVAL85
                        
0185 : 53474e           		fcc	"SGN"
0188 : 0b05             		fdb	EVAL86
018a : 00               		fcb	0
                        
                        * INITIALIZATION
                        
018b : ce0100           CLRBEG		ldx	#START
018e : df0a             		stx	XTEMP3		;SAVE X
0190 : ce000c           CLRBG2		ldx	#DATAST		;SET START
0193 : 2008             		bra	CLEAR		;GO CLEAR
                        
0195 : fe010f           CLREND		ldx	MEMEND	;SET END
0198 : df0a             		stx	XTEMP3	;SAVE
019a : fe0d9d           		ldx	ENDSTR
019d : 4f               CLEAR		clra		;CLEAR ACC.
019e : a700             CLEAR2		sta	0,x	;CLEAR BYTE
01a0 : 08               		inx		;BUMP THE POINTER
01a1 : 9c0a             		cpx	XTEMP3	;DONE?
01a3 : 26f9             		bne	CLEAR2
01a5 : 39               		rts		;RETURN
                        ;
                        ; The original code didn't display a copyright message
                        ; but I wanted to make sure the proper credit was
                        ; given, and also so people know which BASIC is runnin
                        ;
                        	if	SHOW_COPYRIGHT
01a6 : 0d0a0a           STARTMSG	db	CR,LF,LF
01a9 : 4d4943524f2042.. 		db	"MICRO BASIC PLUS",CR,LF
01bb : 434f5059524947.. 		db	"COPYRIGHT (C) 1976 BY",CR,LF
01d2 : 544543484e4943.. 		db	"TECHNICAL SYSTEMS CONSULTANTS"
01ef : 0d0a0a04         		db	CR,LF,LF,EOT
                        	endif
                        
01f3 :                  MICBAS
                        	if	SHOW_COPYRIGHT
01f3 : ce01a6           		ldx	#STARTMSG
01f6 : bd0343           		jsr	PDATA1
AS02 Assembler for M6802 [1.42].                                     Page    7
-------------------------------- MicroBas.asm --------------------------------

                        	endif
                        ;
                        ; Need a better solution.  There's no guarantee that
                        ; a valid function exists in the EXTERN code space,
                        ; so forcing it to an RTS seems natural, but then
                        ; any user-loaded function is blown away on a cold
                        ; start.
                        ;
                        ; This wasn't a problem for a RAM based version
                        ; because the user loaded BASIC which put an RTS
                        ; into EXTERN, then they could load any extensions
                        ; that replaced the RTS with their logic, then
                        ; started BASIC.
                        ;
                        	if	FORCE_RTS
                        		lda	#$39	;RTS
                        		sta	EXTERN
                        	endif
                        ;
01f9 : 8d90             		bsr	CLRBEG	;GO CLEAR
01fb : ce1000           		ldx	#STORSP
01fe : ff0d9d           		stx	ENDSTR	;SET END STORAGE:
0201 : 8d92             		bsr	CLREND	;GO CLEAR
                        
                        * GET LINE INTO INPUT BUFFER
                        
0203 : ce0103           FILBUF		ldx	#RESTRT
0206 : ffa048           		stx	MONPC	;SET UP RETURN POINTER
0209 : 8ea07f           		lds	#STACK
020c : ce0068           		ldx	#BUFFER
020f : df0a             		stx	XTEMP3	;SAVE BOUND
0211 : bd0190           		bsr	CLRBG2
0214 : ce0d9d           		ldx	#ENDSTR	;SET PUHCH LIMITS
0217 : ffa002           		stx	PFILBG
021a : ee00             		ldx	0,x	;SET END
021c : ffa004           		stx	PFILEN
021f : df08             		stx	DIMPNT
0221 : ce0068           		ldx	#BUFFER	;POINT TO BUFFER
0224 : bd033e           		jsr	PCRLF	;OUT A CR & LF
0227 : 8621             		lda	#PRMPTC
0229 : bd049e           		jsr	OUTCH	;OUTPUT PROMPT
022c : bd0324           FILBU2		jsr	INCHAR	;GET A CHARACTER
022f : 27d2             		beq	FILBUF
0231 : a700             		sta	0,x	;SAVE CHAR.
0233 : 810d             		cmpa	#CR	;IS IT A C.R. ?
0235 : 2708             		beq	FILBU6
0237 : 08               		inx		;BUMP THE POINTER
0238 : 8c00b0           		cpx	#BUFFER+72
023b : 26ef             		bne	FILBU2	;END OF BUFFER?
023d : 20c4             		bra	FILBUF
023f : ce0068           FILBU6		ldx	#BUFFER	;RESET POINTER
0242 : bd0385           		jsr	BCDCO1	;LINE NO. CONV.
0245 : df31             		stx	XTEMP2	;SAVE POINTER
0247 : bd03cf           		jsr	FNDKEY	;CHECK KEY WORD
024a : 4d               		tsta
024b : 261a             		bne	FILBU8	;IF NONZERO THEN OK
024d : de04             		ldx	BUFPNT	;POINT TO BUFFER
024f : a600             		lda	0,x	;GET CHARACTER
0251 : 810d             		cmpa	#CR	;IS IT A C.R.?
0253 : 2608             		bne	FILBU7
0255 : d628             		ldab	NOEXFL	;DIR. EXECUTION?
0257 : 27aa             		beq	FILBUF
AS02 Assembler for M6802 [1.42].                                     Page    8
-------------------------------- MicroBas.asm --------------------------------

0259 : 9712             		sta	CRFLAG	;SET FLAG
025b : 200a             		bra	FILBU8	;IT IS OK
025d : bd0798           FILBU7		jsr	TSTLET	;LET?
0260 : 2705             		beq	FILBU8
0262 : 8610             FILB75		lda	#$10
0264 : 7e04b7           		jmp	MISTAK	;REPORT ERROR #0
0267 : 963e             FILBU8		lda	CHRCNT	;GET CHAR. COUNT
0269 : 9026             		suba	NUMCNT	;SUB LINE # DIGITS
026b : 973e             		sta	CHRCNT	;SAVE
026d : d628             		ldab	NOEXFL	;DIRECT EXECUTE ?
026f : 2606             		bne	STUFLN	;IF NOT GO PUT LINE
0271 : bd033e           		jsr	PCRLF	;OUTPUT C.R. L.F.
0274 : 7e0794           		jmp	RUNEX4	;GO TO ROUTINE
                        
                        * PUT LINE IN PROGRAM STORAGE
                        
0277 : fe010f           STUFLN		ldx	MEMEND
027a : df37             		stx	CPX1
027c : de31             		ldx	XTEMP2	;SET POINTER
027e : df04             		stx	BUFPNT	;SAVE POINTER
0280 : bd02f9           		jsr	FNDLIN	;GO FIND LINE IN STORE
0283 : df22             		stx	XSAVE	;SAVE POINTER
0285 : 5d               		tstb		;DID WE FIND IT?
0286 : 2620             		bne	INSERT	;IF NOT GO INSERT
                        
                        
                        * REPLACE EXISTING LINE WITH NEW ONE
                        
0288 : 5c               REPLAC		incb		;INC THE COUNTER
0289 : a600             		lda	0,x	;GET A CHARACTER
028b : 08               		inx		;BUMP THE POINTER
028c : 810d             		cmpa	#CR	;IS IT A C.R,?
028e : 26f8             		bne	REPLAC
0290 : f702a0           REPLA4		stab	OFSET2+1	;SETUP OFFSET
0293 : 86ff             		lda	#$FF	;GET COUNT
0295 : 50               		negb		;2'S COMP. IT
0296 : 8d46             		bsr	ADJEND	;GO FIX END PNTR
0298 : de22             		ldx	XSAVE	;RESTORE THE POINTER
029a : bc0d9d           REPLA5		cpx	ENDSTR	;END OF STORAGE?
029d : 2707             		beq	REPLA6
029f : a600             OFSET2		lda	0,x
02a1 : a700             		sta	0,x	;MOVE A CHARACTER
02a3 : 08               		inx		;BUMP THE POINTER
02a4 : 20f4             		bra	REPLA5	;REPEAT
02a6 : de22             REPLA6		ldx	XSAVE	;RESTORE THE POINTER
                        
                        * INSERT A LINE INTO PROGRAM STORAGE
                        
02a8 : 9612             INSERT		lda	CRFLAG	;LONE C.R. ?
02aa : 262f             		bne	INSER6
02ac : fe0d9d           		ldx	ENDSTR
02af : d63e             		ldab	CHRCNT	;GET CHAR. COUNT
02b1 : cb02             		addb	#2	;BIAS FOR LINE NUM.
02b3 : f702c0           		stab	OFFSET+1	;SETUP OFFSET
02b6 : 8d26             		bsr	ADJEND	;FIX END PNTR
02b8 : 9c22             INSER2		cpx	XSAVE	;DONE?
02ba : 2707             		beq	INSER3
02bc : 09               		dex		;DEC THE POINTER
02bd : a600             		lda	0,x	;GET A CHAR,
02bf : a700             OFFSET		sta	0,x
02c1 : 20f5             		bra	INSER2	;MOVE IT
02c3 : 09               INSER3		dex
AS02 Assembler for M6802 [1.42].                                     Page    9
-------------------------------- MicroBas.asm --------------------------------

02c4 : bd06bb           		jsr	PUTLB2	;PUT LAB
02c7 : 08               		inx		;BUMP THE POINTER
02c8 : 08               		inx
02c9 : df22             INSER4		stx	XSAVE	;SAVE POINTER
02cb : de04             		ldx	BUFPNT
02cd : a600             		lda	0,x	;GET CHAR*
02cf : 08               		inx		;BUMP THE POINTER
02d0 : df04             		stx	BUFPNT	;SAVE
02d2 : de22             		ldx	XSAVE	;RESTOR PNTR
02d4 : 08               		inx
02d5 : a700             		sta	0,x	;SAVE IT
02d7 : 810d             		cmpa	#CR	;IS IT A C.R.?
02d9 : 26ee             		bne	INSER4
02db : 7e0203           INSER6		jmp	FILBUF	;60 TO MAIN LOOP
                        
                        * ADJUST THE END OF PROGRAM POINTER
                        
02de : fb0d9e           ADJEND		addb	ENDSTR+1
02e1 : b90d9d           		adca	ENDSTR	;ADD IN VALUE
02e4 : d73a             		stab	CPX2+1
02e6 : 9739             		sta	CPX2	;SET END POINTER
02e8 : bd0d03           		jsr	CMPX1
02eb : 2407             		bcc	ADJEN2
02ed : f70d9e           		stab	ENDSTR+1
02f0 : b70d9d           		sta	ENDSTR	;SAVE NEW POINTER
02f3 : 39               		rts		;RETURN
02f4 : 8690             ADJEN2		lda	#$90	;SET ERROR
02f6 : 7e04b7           		jmp	MISTAK
                        
                        * TRY TO FIND LINE
                        
02f9 : 9664             FNDLIN		lda	NUMBER+2
02fb : d663             		ldab	NUMBER+1
02fd : ce1000           FINDLN		ldx	#STORSP	;SETUP POINTER
0300 : bc0d9d           FINDL1		cpx	ENDSTR	;END OF STORAGE?
0303 : 2602             		bne	FINDL4
0305 : 5c               FINDL2		incb
0306 : 39               		rts		;RETURN
0307 : e100             FINDL4		cmpb	0,x	;CHECK M.S. DIGITS
0309 : 220a             		bhi	FINDL6
030b : 26f8             		bne	FINDL2
030d : a101             		cmpa	1,x	;CHECK L.S, DIGITS
030f : 2204             		bhi	FINDL6
0311 : 26f2             		bne	FINDL2
0313 : 5f               		clrb		;CEAR FLAG
0314 : 39               		rts		;RETURN
0315 : 8d03             FINDL6		bsr	FNDCRT	;GO FIND C.R,
0317 : 08               		inx		;BUMP THE POINTER
0318 : 20e6             		bra	FINDL1	;REPEAT
                        
                        * FIND A C,R, IN STORAGE
                        
031a : 36               FNDCRT		psha		;SAVE A
031b : 860d             		lda	#CR
031d : 08               FNDVAL		inx		;BUMP THE POINTER
031e : a100             		cmpa	0,x	;TEST FOR C.R.
0320 : 26fb             		bne	FNDVAL
0322 : 32               		pula		;RESTORE A
0323 : 39               		rts		;RETURN
                        
                        * INPUT
                        
AS02 Assembler for M6802 [1.42].                                     Page   10
-------------------------------- MicroBas.asm --------------------------------

0324 : bd0109           INCHAR		jsr	INCH	;GET THE CHAR.
0327 : 8108             		cmpa	#BACKSP	;IS IT A BACKSPACE?
0329 : 260b             		bne	INCHR2
032b : 8c0068           		cpx	#BUFFER	;BEGINNING OF BUF ?
032e : 270d             		beq	INCHR4
0330 : 09               		dex		;BACKUP ONE POS.
0331 : 7a003e           		dec	CHRCNT	;DEC CHAR. COUNT
0334 : 20ee             		bra	INCHAR
0336 : 8118             INCHR2		cmpa	#DELCOD	;DELETE LINE ?
0338 : 2703             		beq	INCHR4
033a : 7c003e           		inc	CHRCNT
033d : 39               INCHR4		rts		;RETURN
                        
                        * PRINT CARRIAGE RETURN & LINEFEED
                        
033e : df22             PCRLF		stx	XSAVE	;SAVE X REG
0340 : ce0355           		ldx	#CRLFST	;POINT TO STRING
0343 : a600             PDATA1		lda	0,x	;GET CHAR
0345 : 8104             		cmpa	#EOT	;IS IT 4?
0347 : 2706             		beq	PCRLF2
0349 : bd049e           		jsr	OUTCH	;OUTPUT CHAR
034c : 08               		inx		;BUMP THE POINTER
034d : 20f4             		bra	PDATA1	;REPEAT
034f : de22             PCRLF2		ldx	XSAVE	;RESTORE X REG
0351 : 7f001d           		clr	FLDCNT	;ZERO FIELD COUNT
0354 : 39               		rts		;RETURN
                        
0355 : 0d0a0000000004   CRLFST		fcb	CR,LF,0,0,0,0,EOT
                        
                        * TEST FOR STATEMENT TERMINATOR
                        
035c : 810d             TSTTRM		cmpa	#CR	;C,R,?
035e : 2702             		beq	TSTTR2
0360 : 813a             		cmpa	#':'	;COLON?
0362 : 39               TSTTR2		rts		;RETURN
                        
                        * CLEAR NUMBER THROUGH NUMBER+2
                        
0363 : bd0ba2           UPSCLR		jsr	STAKUP
0366 : 4f               CLRNUM		clra
0367 : 9762             		sta	NUMBER
0369 : 9763             		sta	NUMBER+1
036b : 9764             		sta	NUMBER+2
036d : 39               		rts
                        
                        * CONVERT NUMBER TO PACKED BCD
                        
036e : 8df6             BCDCON		bsr	CLRNUM	;CLEAR NUMBER
0370 : 9728             		staa	NOEXFL
0372 : 9727             		staa	NEGFLG
0374 : 9726             		staa	NUMCNT
0376 : bd03bc           		jsr	SKIPSP	;SKIP SPACES
0379 : 812b             		cmpa	#'+'	;IS IT A +?
037b : 2707             		beq	BCDC01
037d : 812d             		cmpa	#'-'	;IS IT A - ?
037f : 2604             		bne	BCDCO1
0381 : 730027           		com	NEGFLG	;SET FLAG
0384 : 08               BCDC01		inx
0385 : bd0d33           BCDCO1		jsr	CLASS	;GET A DIGIT
0388 : c103             		cmpb	#3	;IS IT A NUMBER?
038a : 2705             		beq	BCDCO2
038c : 9627             		lda	NEGFLG
AS02 Assembler for M6802 [1.42].                                     Page   11
-------------------------------- MicroBas.asm --------------------------------

038e : 7e0c3b           		jmp	FIXSIN	;GO FIX UP THE SIGN
0391 : 08               BCDCO2		inx		;BUMP THE POINTER
0392 : 9728             		staa	NOEXFL	;SET NO EXEC FLU
0394 : 840f             		anda	#$0F	;MASK OFF ASCII
0396 : c604             		ldab	#4	;SET COUNTER
0398 : 780064           BCDCO4		asl	NUMBER+2
039b : 790063           		rol	NUMBER+1
039e : 790062           		rol	NUMBER	;SHIFT PREV. OVER
03a1 : 5a               		decb		;DEC THE COUNTER
03a2 : 26f4             		bne	BCDCO4
03a4 : 9b64             		adda	NUMBER+2
03a6 : 9764             		staa	NUMBER+2	;SAVE NEW VALUE
03a8 : 7c0026           		inc	NUMCNT	;INC NUMBER CNTR
03ab : 20d8             		bra	BCDCO1
                        
                        * FIND NEXT BLOCK
                        
03ad : de04             NXTBLK		ldx	BUFPNT	;RESTORE POINTER
03af : a600             NXTBL4		lda	0,x	;GET A CHAR.
03b1 : 8120             		cmpa	#' '	;IS IT A SPACE?
03b3 : 2707             		beq	SKIPSP
03b5 : 08               		inx		;BUMP THE POINTER
03b6 : 20f7             		bra	NXTBL4	;REPEAT
                        
                        * CONVERT AND SKIP
                        
03b8 : 8db4             CONSKP		bsr	BCDCON
03ba : 09               		dex
                        
                        * SKIP ALL SPACES
                        
03bb : 08               SKPSP0		inx
03bc : a600             SKIPSP		lda	0,x	;GET CHR FROM BUF
03be : 8120             		cmpa	#' '	;IS IT A SPACE?
03c0 : 27f9             		beq	SKPSP0
03c2 : 39               SKIPS4		rts		;RETURN
                        
                        * FIND NEXT BLOCK NOT EXPECTING A SPACE
                        
03c3 : de04             NXTSPC		ldx	BUFPNT	;SET POINTER
03c5 : bd0d33           NXTSP4		jsr	CLASS	;GO CLASSIFY
03c8 : c102             		cmpb	#2	;IS IT A LETTER?
03ca : 26f0             		bne	SKIPSP
03cc : 08               		inx		;BUMP THE POINTER
03cd : 20f6             		bra	NXTSP4
                        
                        * FIND KEY WORD IF POSSIBLE
                        
                        ;	noopt
03cf : 8deb             FNDKEY		jsr	SKIPSP	;SKIP SPACES
                        ;	opt
03d1 : df04             		stx	BUFPNT	;SAVE THE POINTER
03d3 : df22             		stx	XSAVE
03d5 : ce0111           		ldx	#KEYTBL	;POINT TO KEY WORDS
03d8 : c605             FNDKE2		ldab	#5
03da : a100             FNDKE4		cmpa	0,x	;TEST THE CHARACTER
03dc : 2612             		bne	FNDKE6
03de : df0a             		stx	XTEMP3	;SAVE POINTER
03e0 : de22             		ldx	XSAVE
03e2 : 08               		inx		;BUMP POINTER
03e3 : a600             		lda	0,x	;GET CHAR.
03e5 : df22             		stx	XSAVE
AS02 Assembler for M6802 [1.42].                                     Page   12
-------------------------------- MicroBas.asm --------------------------------

03e7 : de0a             		ldx	XTEMP3	;REST. PNTR.
03e9 : 08               		inx
03ea : 5a               		decb
03eb : c102             		cmpb	#2
03ed : 26eb             		bne	FNDKE4	;IF NOT DONE REPEAT
03ef : 39               FNDKE5		rts		;RETURN
03f0 : 08               FNDKE6		inx		;BUMP THE COUNTER
03f1 : 5a               		decb
03f2 : 26fc             		bne	FNDKE6
03f4 : a600             		lda	0,x	;GET CHARACTER
03f6 : 27f7             		beq	FNDKE5	;IF ZERO, END OF LIST
03f8 : df0a             		stx	XTEMP3	;SAVE POINTER
03fa : de04             		ldx	BUFPNT
03fc : df22             		stx	XSAVE
03fe : a600             		lda	0,x	;GET NEW CHAR.
0400 : de0a             		ldx	XTEMP3	;RESTORE POINTER
0402 : 20d4             		bra	FNDKE2	;REPEAT
                        
                        
                        * OUTPUT A NUMBER FROM PACKED BCD BYTES
                        
0404 : ce0062           OUTBCD		ldx	#NUMBER	;SET POINTER
0407 : c602             OUTBCI		ldab	#2	;SET COUNTER
0409 : 0c               		clc
040a : a600             		lda	0,x	;GET A WORD
040c : 2a19             		bpl	OUTBC4	;IF NOT NEG JMP AHEAD
040e : 862d             		lda	#'-'
0410 : bd049e           		jsr	OUTCH	;OUTPUT A
0413 : 7c001d           		inc	FLDCNT
0416 : 200f             		bra	OUTBC4
0418 : a600             OUTBC2		lda	0,x	;GET DIGITS
041a : 85f0             		bita	#$F0	;MASK
041c : 2502             		bcs	OUTBC3
041e : 2707             		beq	OUTBC4	;JMP IF ZEROES
0420 : bd0496           OUTBC3		jsr	OUTHL	;OUTPUT A DIGIT
0423 : 7c001d           		inc	FLDCNT
0426 : 0d               		sec
0427 : a600             OUTBC4		lda	0,x	;GET A DIGIT
0429 : c5ff             		bitb	#$FF	;LAST DIGIT?
042b : 2706             		beq	OUTBC6
042d : 850f             		bita	#$0F	;MASK
042f : 2502             		bcs	OUTBC6
0431 : 2707             		beq	OUTBC8	;JMP IF ZEROES
0433 : bd049a           OUTBC6		jsr	OUTHR	;OUTPUT A DIGIT
0436 : 7c001d           		inc	FLDCNT
0439 : 0d               		sec
043a : 08               OUTBC8		inx		;BUMP THE POINTER
043b : 5a               		decb		;DEC THE COUNTER
043c : 2ada             		bpl	OUTBC2	;REPEAT IF NOT DONE
043e : 39               		rts		;RETURN
                        
                        * LIST USERS PROGRAM
                        
                        ;	noopt
043f : 8d82             LIST		jsr	NXTSPC	;FIND NEXT
                        ;	opt
0441 : 810d             		cmpa	#CR
0443 : 2725             		beq	LIST3
0445 : bd036e           		jsr	BCDCON	;GET LINE NUM
0448 : df04             		stx	BUFPNT	;SAVE POINTER
044a : bd02f9           		jsr	FNDLIN	;FIND LINE
044d : df22             		stx	XSAVE	;SAVE IT
AS02 Assembler for M6802 [1.42].                                     Page   13
-------------------------------- MicroBas.asm --------------------------------

044f : bd03c3           		jsr	NXTSPC
0452 : 810d             		cmpa	#CR	;C.R.?
0454 : 2605             		bne	LIST1
0456 : 7c001b           		inc	SUBCNT	;SET TO 1
0459 : 200b             		bra	LIST2
045b : 08               LIST1		inx		;BUMP THE POINTER
045c : bd03bc           		jsr	SKIPSP
045f : bd036e           		jsr	BCDCON	;GET COUNT
0462 : 9664             		lda	NUMBER+2
0464 : 971b             		sta	SUBCNT	;SAVE IT
0466 : de22             LIST2		ldx	XSAVE	;POINT TO LINE
0468 : 2003             		bra	LIST4
046a : ce1000           LIST3		ldx	#STORSP	;SET POINTER
046d : bc0d9d           LIST4		cpx	ENDSTR	;END OF STORAGE?
0470 : 2721             		beq	LIST8
0472 : bd033e           		jsr	PCRLF	;OUTPUT A
0475 : c601             		ldab	#1	;SETUP COUNTER
0477 : 0c               		clc
0478 : 8d9e             		bsr	OUTBC2	;OUT LINE NUMBER
047a : a600             LIST5		lda	0,x	;GET A CHARACTER
047c : 810d             		cmpa	#CR	;IS IT A C.R.?
047e : 2705             		beq	LIST6
0480 : 8d1c             		bsr	OUTCH	;OUTPUT CHARACTER
0482 : 08               		inx		;BUMP THE POINTER
0483 : 20f5             		bra	LIST5	;REPEAT
0485 : 08               LIST6		inx		;BUMP THE POINTER
0486 : 961b             		lda	SUBCNT	;GET COUNT
0488 : 27e3             		beq	LIST4
048a : 8b99             		adda	#$99	;DEC THE COUNT
048c : 19               		daa
048d : 2704             		beq	LIST8
048f : 971b             		sta	SUBCNT	;SAVE
0491 : 20da             		bra	LIST4
0493 : 7e0203           LIST8		jmp	FILBUF
                        
0496 : 44               OUTHL		lsra
0497 : 44               		lsra
0498 : 44               		lsra
0499 : 44               		lsra		;MOVE TO BOTTOM
049a : 840f             OUTHR		anda	#$0F	;MASK
049c : 8b30             		adda	#'0'	;BIAS
049e : bd010c           OUTCH		jsr	BREAK	;CHECK FOR BREAK
04a1 : 7e0106           		jmp	OUTEEE	;GO PRINT
                        
                        * INTERNAL BREAK ROUTINE
                        
04a4 : 36               INTBRK		psha		;always save/restore A
                        	if USE_ACIA
                        ;
                        ; For an ACIA, see if there is a character.  If not, j
                        ; return.  Otherwise get the character and see if it i
                        ; CTRL-C.  Continue if not, abort if yes.
                        ;
04a5 : b68004           		lda	ACIASTAT	;get status register
04a8 : 8401             		anda	#$01	;has input character?
04aa : 2602             		bne	gotchar	;branch if so
04ac : 32               aciaout		pula		;else restore...
04ad : 39               		rts		;...and return
04ae : b68005           gotchar		lda	ACIADATA	;read character
04b1 : 8103             		cmpa	#$03	;CTRL-C?
04b3 : 26f7             		bne	aciaout
                        	else
AS02 Assembler for M6802 [1.42].                                     Page   14
-------------------------------- MicroBas.asm --------------------------------

                        		lda	PIAADR	;CHECK
                        		bpl	BREAK2
                        		pula		;GET CHAR
                        		rts		;RETURN
                        BREAK2		lda	PIAADR
                        		bpl	BREAK2
                        	endif
04b5 : 8699             		lda	#$99	;"BREAK" error
                        
                        * OUTPUT ERROR MESSAGE
                        
04b7 : 36               MISTAK		psha		;SAVE A
04b8 : bd033e           		jsr	PCRLF	;OUTPUT A CR & LF
04bb : ce04ec           MISTA1		ldx	#ERRSTR	;POINT TO ERROR STRING
04be : bd0343           		jsr	PDATA1	;OUTPUT IT
04c1 : 32               		pula		;RESTORE A
04c2 : 36               		psha		;SAVE A
                        ;	noopt
04c3 : 8dd1             		jsr	OUTHL	;OUTPUT DIGIT
04c5 : 32               MISTA2		pula		;RESTORE A
04c6 : 8dd2             		jsr	OUTHR	;OUT 1'S DIGIT
                        ;	opt
04c8 : d619             		ldab	RUNFLG	;RUNNING?
04ca : 2603             		bne	RUNER1
04cc : 7e0203           MISTA4		jmp	FILBUF
04cf : ce04f5           RUNER1		ldx	#ERSTR2	;POINT TO STRING
04d2 : bd0343           		jsr	PDATA1	;OUTPUT IT
04d5 : de04             		ldx	BUFPNT	;SET POINTER
04d7 : 09               RUNER2		dex		;DEC THE POINTER
04d8 : 8c1000           		cpx	#STORSP	;BEGINNING?
04db : 2707             		beq	RUNER4
04dd : a600             		lda	0,x	;GET CHAR
04df : 810d             		cmpa	#CR	;C.R.?
04e1 : 26f4             		bne	RUNER2
04e3 : 08               		inx		;BUMP THE POINTER
04e4 : c601             RUNER4		ldab	#1
04e6 : 0c               		clc
04e7 : bd0418           		jsr	OUTBC2	;OUT LINE NUM.
04ea : 20e0             		bra	MISTA4
04ec : 07               ERRSTR		fcb	BEL
04ed : 4552524f522023   		fcc	"ERROR #"
04f4 : 04               		fcb	EOT
                        
04f5 : 20415420         ERSTR2		fcc	" AT "
04f9 : 04               		fcb	EOT
                        
                        * PRINT ROUTINE
                        
04fa : bd03c3           PRINT		jsr	NXTSPC	;FIND NEXT BLOCK
04fd : bd035c           PRINT0		jsr	TSTTRM
0500 : 2603             		bne	FIELD1
0502 : 7e058f           		jmp	PRINT8
0505 : 7f0012           FIELD1		clr	CRFLAG
0508 : 812c             		cmpa	#','	;IS IT A ","
050a : 261f             		bne	PRINT2
050c : d61d             		ldab	FLDCNT	;GET COUNT
050e : 8620             FIELD2		lda	#' '	;SPACE
                        ;	noopt
0510 : 8d8c             		jsr	OUTCH	;OUTPUT A SPACE
                        ;	opt
0512 : 5c               		incb
0513 : c507             		bitb	#7	;END OF FIELD?
AS02 Assembler for M6802 [1.42].                                     Page   15
-------------------------------- MicroBas.asm --------------------------------

0515 : 26f7             		bne	FIELD2
0517 : c147             		cmpb	#$47	;END OF LINE?
0519 : 2204             		bhi	FIELD3
051b : d71d             		stab	FLDCNT	;SAVE FIELD INFO
051d : 2003             		bra	PRINT1
051f : bd033e           FIELD3		jsr	PCRLF	;OUT A C.R. & L.F.
0522 : 7c0012           PRINT1		inc	CRFLAG	;SET FLAG
0525 : 08               		inx		;BUMP THE POINTER
0526 : bd03bc           		jsr	SKIPSP
0529 : 20d2             		bra	PRINT0
052b : 813b             PRINT2		cmpa	#';'	;IS IT A ";"
052d : 27f3             		beq	PRINT1
052f : 8122             		cmpa	#'"'	;IS IT A QUOTE?
0531 : 2605             		bne	PRINT4
0533 : 08               		inx		;BUMP THE POINTER
0534 : 8d64             		bsr	PSTRNG	;OUTPUT STRING
0536 : 2049             		bra	PRINT6
0538 : 7f0017           PRINT4		clr	TABFLG	;CLEAR FLAG
053b : 8154             		cmpa	#'T'	;IS IT A T?
053d : 2606             		bne	PRIN45
053f : 9717             		sta	TABFLG	;SET FLAG
0541 : 8641             		lda	#'A'
0543 : 2006             		bra	PRIN47
0545 : 8153             PRIN45		cmpa	#'S'	;IS IT A S?
0547 : 262e             		bne	PRIN55
0549 : 8650             		lda	#'P'
054b : a101             PRIN47		cmpa	1,x
054d : 2628             		bne	PRIN55
054f : bd03c5           		jsr	NXTSP4	;FIND NEXT
0552 : bd0a77           		jsr	EXPR	;EVALUATE
0555 : bd0671           		jsr	BINCON	;CONVERT
0558 : d664             		ldab	NUMBER+2
055a : 2725             		beq	PRINT6
055c : 9617             		lda	TABFLG	;CHECK FLAG
055e : 2707             		beq	PRINT5
0560 : 5a               		decb
0561 : d11d             		cmpb	FLDCNT	;CHECK COUNT
0563 : 231c             		bls	PRINT6
0565 : 2002             		bra	PRIN51
0567 : db1d             PRINT5		addb	FLDCNT
0569 : 8620             PRIN51		lda	#' '	;SPACE
056b : bd049e           		jsr	OUTCH	;OUTPUT SPACE
056e : 7c001d           		inc	FLDCNT	;BUMP COUNTER
0571 : d11d             		cmpb	FLDCNT
0573 : 26f4             		bne	PRIN51	;REPEAT
0575 : 200a             PRIN52		bra	PRINT6
0577 : bd0a77           PRIN55		jsr	EXPR	;EVAL EXPRESSION
057a : df22             		stx	XSAVE	;SAVE POINTER
057c : bd0404           		jsr	OUTBCD	;OUTPUT VALUE
057f : de22             		ldx	XSAVE	;RESTORE
0581 : bd0d2e           PRINT6		jsr	SKYCLS
0584 : 5a               		decb
0585 : 2603             		bne	PRINT7	;CHECK FOR ERROR
0587 : 7e04fd           		jmp	PRINT0
058a : 8631             PRINT7		lda	#$31
058c : 7e04b7           		jmp	MISTAK
058f : 7d0012           PRINT8		tst	CRFLAG	;C.R. ?
0592 : 2603             		bne	PRINT9
0594 : bd033e           		jsr	PCRLF	;OUTPUT C.R. L.F
0597 : 7e0757           PRINT9		jmp	RUNEXC
                        
                        * PRINT STRING ROUTINE
AS02 Assembler for M6802 [1.42].                                     Page   16
-------------------------------- MicroBas.asm --------------------------------

                        
059a : a600             PSTRNG		lda	0,x	;GET A CHAR.
059c : 8122             		cmpa	#'"'	;IS IT A QUOTE?
059e : 270e             		beq	PSTRN4
05a0 : bd035c           		jsr	TSTTRM	;IS IT A C.R.?
05a3 : 270d             		beq	PSTRN8
05a5 : bd049e           		jsr	OUTCH	;OUTPUT CHARACTER
05a8 : 7c001d           		inc	FLDCNT	;BUMP FIELD CNT
05ab : 08               		inx		;BUMP THE POINTER
05ac : 20ec             		bra	PSTRNG	;REPEAT
05ae : 08               PSTRN4		inx
05af : 7e03bc           		jmp	SKIPSP
05b2 : 8632             PSTRN8		lda	#$32
05b4 : 7e04b7           		jmp	MISTAK	;REPORT ERROR
                        
                        * FIND LABLE ROUTINE
                        
05b7 : df04             FNDVAR		stx	BUFPNT	;SAVE POINTER
05b9 : bd0d35           		jsr	CLASS1	;GO CLASSIFY CHAR.
05bc : c102             		cmpb	#2	;CHECK FOR LETTER
05be : 262f             		bne	FNDL25	;ERROR
05c0 : 7f0020           		clr	XTEMP
05c3 : 16               		tab		;SAVE LABLE
05c4 : 48               		asla		;MULT IT BY 2
05c5 : 1b               		aba		;ADD IT
05c6 : 8013             		suba	#$13
05c8 : 9721             		sta	XTEMP+1
05ca : de20             		ldx	XTEMP	;POINT TO IT
05cc : 39               		rts		;RETURN
                        
                        * FIND DIMENSIONED VARIABLE
                        
05cd : a600             FNDLB0		lda	0,x
05cf : 08               FNDLBL		inx	;ADVANCE POINTER
05d0 : 7f0018           		clr	DIMFLG
05d3 : 8de2             		bsr	FNDVAR	;GO FIND VAR.
05d5 : 5f               		clrb
05d6 : a600             		lda	0,x	;GET CHAR.
05d8 : 810a             		cmpa	#$0A	;CHECK FOR 1 DIM
05da : 2706             		beq	FNDLB2
05dc : 810b             		cmpa	#$0B	;CHECK IF 2 DIM
05de : 2701             		beq	FNDLB1
05e0 : 39               		rts
05e1 : 5c               FNDLB1		incb	;SET FLAG-2 DIM
05e2 : a601             FNDLB2		lda	1,x	;SET POINTER
05e4 : 36               		psha
05e5 : a602             		lda	2,x
05e7 : 36               		psha
05e8 : 37               		pshb		;SAVE B
05e9 : bd03c3           		jsr	NXTSPC	;FIND NEXT
05ec : 33               		pulb
05ed : 8128             		cmpa	#'('	;IS IT A PAREN?
05ef : 2671             FNDL25		bne	FNDLB9
05f1 : 5d               		tstb
05f2 : 2713             		beq	FNDLB3
05f4 : 08               		inx
05f5 : bd0a7a           		jsr	EXPRO		;GO EVALUATE
05f8 : 9664             		lda	NUMBER+2	;GET RESULT
05fa : 36               		psha			;SAVE IT
05fb : bd0bb3           		jsr	STAKDN		;RESTORE
05fe : bd03c3           		jsr	NXTSPC		;FIND NEXT
0601 : 812c             		cmpa	#','		;IS IT A COMMA?
AS02 Assembler for M6802 [1.42].                                     Page   17
-------------------------------- MicroBas.asm --------------------------------

0603 : 265d             		bne	FNDLB9
0605 : 2002             		bra	FNDLB4
0607 : 4f               FNDLB3		clra
0608 : 36               		psha		;SET ROWV
0609 : 4c               FNDLB4		inca
060a : 9718             		sta	DIMFLG	;SET FLAG
060c : 08               		inx
060d : bd0a7a           		jsr	EXPRO
0610 : 08               		inx
0611 : df04             		stx	BUFPNT	;SAVE POINTER
0613 : 32               		pula
0614 : 9714             		sta	ROWVAR	;SAVE
0616 : 32               		pula
0617 : 9721             		sta	XTEMP+1	;SAVE
0619 : 32               		pula
061a : 9720             		sta	XTEMP	;SAVE
061c : de20             		ldx	XTEMP	;SET POINTER
061e : a600             		lda	0,x	;GET CHAR
0620 : 9716             		sta	COLCON	;SAVE IT
0622 : 08               		inx		;BUMP THE POINTER
0623 : 08               		inx
0624 : df20             		stx	XTEMP
0626 : bd0363           		jsr	UPSCLR
0629 : 9614             		lda	ROWVAR	;GET VAR.
062b : de20             		ldx	XTEMP
062d : 09               		dex		;DEC POINTER
062e : a100             		cmpa	0,x	;CHECK
0630 : 2230             		bhi	FNDLB9
0632 : 9764             		sta	NUMBER+2
0634 : bd0363           		jsr	UPSCLR	;PUSH STACK
0637 : 9616             		lda	COLCON	;GET CONST,
0639 : 915e             		cmpa	AC-1	;CHECK
063b : 2702             		beq	FNDL45
063d : 2323             		bls	FNDLB9	;ERROR!
063f : 8b01             FNDL45		adda	#1
0641 : 19               		daa		;BIAS IT
0642 : 9764             		sta	NUMBER+2
0644 : bd0c45           		jsr	MULT	;GO MULTIPLY
0647 : bd0c1b           		jsr	ADD	;GO ADD
064a : bd0667           FNDLB5		jsr	TIMTHR
                        
                        * ROUTINE TO ADD VALUE TO X-REG.
                        
064d : 9620             ADDX		lda	XTEMP	;GET M.S.BYTE
064f : d621             		ldab	XTEMP+1
0651 : db64             		addb	NUMBER+2
0653 : 9963             		adca	NUMBER+1
0655 : 9720             		sta	XTEMP	;SAVE SUM
0657 : d721             		stab	XTEMP+1
0659 : bd0bb3           		jsr	STAKDN
065c : de20             		ldx	XTEMP	;SET POINTER
065e : 7f0018           		clr	DIMFLG	;RESTORE FLAG
0661 : 39               		rts		;RETURN
                        
0662 : 8614             FNDLB9		lda	#$14	;SET ERROR
0664 : 7e04b7           		jmp	MISTAK	;GO REPORT
                        
                        * ROUTINE TO MULTIPLY BY 3
                        
0667 : bd0363           TIMTHR		jsr	UPSCLR
066a : 8603             		lda	#$3	;SET MULTIPLIER
066c : 9764             		sta	NUMBER+2
AS02 Assembler for M6802 [1.42].                                     Page   18
-------------------------------- MicroBas.asm --------------------------------

066e : bd0c45           		jsr	MULT	;GO MULTIPLY
                        
                        * BCD TO BINARY CONVERT.
                        
0671 : 9664             BINCON		lda	NUMBER+2	;GET LS BYTE
0673 : 36               		psha		;SAVE
0674 : 9663             		lda	NUMBER+1
0676 : 36               		psha		;SAVE:
0677 : 5f               		clrb
0678 : d763             		stab	NUMBER+1
067a : d764             		stab	NUMBER+2	;INITIALIZE
067c : 9662             		lda	NUMBER
067e : 8d12             		bsr	ADSHF1	;ADD AND SHIFT
0680 : 32               		pula
0681 : 36               		psha
0682 : 8d0a             		bsr	ADSHF0	;GO ADD IN AND SHIFT
0684 : 32               		pula		;GET MS BYTE AGAIN
0685 : 8d0b             		bsr	ADSHF1	;GO ADD IN AND SHIFT
0687 : 32               		pula		;GET LS BYTE
0688 : 36               		psha
0689 : 8d03             		bsr	ADSHF0
068b : 32               		pula
068c : 201d             		bra	ADDIN	;G0 ADD IN ONES
068e : 44               ADSHF0		lsra
068f : 44               		lsra
0690 : 44               		lsra
0691 : 44               		lsra		;MOVE TO LS HALF
0692 : 8d17             ADSHF1		bsr	ADDIN	;GO ADD IN
0694 : d663             		ldab	NUMBER+1
0696 : 48               		asla
0697 : 59               		rolb		;MULT BY 2
0698 : 37               		pshb
0699 : 36               		psha		;SAVE
069a : 48               		asla
069b : 59               		rolb
069c : 48               		asla
069d : 59               		rolb		;MULT BY 4, =*8
069e : 9764             		sta	NUMBER+2
06a0 : 32               		pula
06a1 : d763             		stab	NUMBER+1
06a3 : 8d08             		bsr	ADDIN1	;GO ADD IN
06a5 : 32               		pula
06a6 : 9b63             		adda	NUMBER+1
06a8 : 9763             		sta	NUMBER+1	;MULTIPLY BY TEN
06aa : 39               		rts
06ab : 840f             ADDIN		anda	#$0F	;MASK
06ad : 9b64             ADDIN1		adda	NUMBER+2
06af : 9764             		sta	NUMBER+2
06b1 : 2403             		bcc	ADDIN2	;CHECK FOR CARRY
06b3 : 7c0063           		inc	NUMBER+1
06b6 : 39               ADDIN2		rts
                        
                        * PUT LABLE ROUTINE
                        
06b7 : 9662             PUTLBL		lda	NUMBER
06b9 : a700             		sta	0,x	;PUT M.S. BYTE
06bb : 9663             PUTLB2		lda	NUMBER+1	;BOB - this was just NUMBER in or
06bd : a701             		sta	1,x	;PUT NEXT
06bf : 9664             		lda	NUMBER+2
06c1 : a702             		sta	2,x	;PUT L.S. BYTE
06c3 : 39               		rts		;RETURN
                        
AS02 Assembler for M6802 [1.42].                                     Page   19
-------------------------------- MicroBas.asm --------------------------------

                        * DIMENSION
                        
06c4 : de06             DIM		ldx	FORSTK	;SET BOUNDS
06c6 : df37             		stx	CPX1
06c8 : bd03c3           		jsr	NXTSPC
06cb : bd03bc           DIMN		jsr	SKIPSP	;CLASSIFY
06ce : bd05b7           		jsr	FNDVAR
06d1 : df0a             		stx	XTEMP3	;SAVE IT
06d3 : bd03c3           		jsr	NXTSPC	;GET TO NEXT
06d6 : 8128             		cmpa	#'('	;IS IT A PARENT
06d8 : 2620             		bne	DIM9
06da : 08               DIM01		inx		;BUMP THE POINTER
06db : bd03b8           		jsr	CONSKP	;CONVERT DIM
06de : 8129             		cmpa	#')'	;IS IT A PAREN
06e0 : 2605             		bne	DIM1
06e2 : 4f               		clra
06e3 : 5f               		clrb
06e4 : 36               		psha		;SAVE IT
06e5 : 2018             		bra	DIM2
06e7 : 812c             DIM1		cmpa	#','	;COMMA?
06e9 : 260f             		bne	DIM9	;ERROR!
06eb : 9664             		lda	NUMBER+2
06ed : 270b             		beq	DIM9
06ef : 36               		psha		;SAVE
06f0 : 08               		inx		;BUMP THE POINTER
06f1 : bd03b8           		jsr	CONSKP	;CONVERT
06f4 : c601             		ldab	#1
06f6 : 8129             		cmpa	#')'	;PAREN?
06f8 : 2705             		beq	DIM2
06fa : 8640             DIM9		lda	#$40	;SET ERROR
06fc : 7e04b7           		jmp	MISTAK	;REPORT
06ff : 9664             DIM2		lda	NUMBER+2
0701 : 27f7             		beq	DIM9
0703 : 36               		psha		;SAVE
0704 : df04             		stx	BUFPNT	;SAVE POINTER
0706 : de0a             		ldx	XTEMP3	;SET X
0708 : 860a             		lda	#$0A
070a : 1b               		aba		;SET MARKER
070b : a700             		staa	0,x	;SAVE IT
070d : 9608             		lda	DIMPNT	;GET POINTER
070f : a701             		staa	1,x	;SAVE IT
0711 : 9609             		lda	DIMPNT+1
0713 : a702             		staa	2,x
0715 : de08             		ldx	DIMPNT	;SET POINTER
0717 : 32               		pula
0718 : a700             		staa	0,x	;SAVE 1ST DIM
071a : 08               		inx		;BUMP THE POINTER
071b : 33               		pulb
071c : e700             		stab 0,x	;SAVE 2ND DIM
071e : 08               		inx
071f : df20             		stx	XTEMP	;SAVE POINTER
0721 : 8b01             		adda	#1
0723 : 19               		daa		;BIAS
0724 : 36               		psha
0725 : 17               		tba
0726 : 8b01             		adda	#1	;BIAS
0728 : 19               		daa		;ADJUST
0729 : 16               		tab		;SAVE
072a : bd0366           		jsr	CLRNUM	;CLEAR STORAGE
072d : d764             		stab	NUMBER+2
072f : bd0363           		jsr	UPSCLR	;GO CLEAR
0732 : 32               		pula
AS02 Assembler for M6802 [1.42].                                     Page   20
-------------------------------- MicroBas.asm --------------------------------

0733 : 9764             		staa	NUMBER+2
0735 : bd0c45           		jsr	MULT	;MULTIPLY
0738 : bd064a           		jsr	FNDLB5	;GO FIX X
073b : bd0d01           		jsr	CMPX	;TEST BOUNDS
073e : 2303             		bls	DIM5
0740 : 7e02f4           		jmp	ADJEN2
0743 : df08             DIM5		stx	DIMPNT	;SAVE RESULT
0745 : de04             		ldx	BUFPNT	;RESTORE F'NTR
0747 : 08               		inx
0748 : bd03bc           		jsr	SKIPSP	;SKIP SPACES
074b : bd035c           		jsr	TSTTRM
074e : 2707             		beq	RUNEXC
0750 : 08               		inx		;BUMP THE POINTER
0751 : 7e06cb           		jmp	DIMN
                        
                        * EXTERNAL ROUTINE JUMP
                        
0754 : bd1f00           EXTRNL		jsr	EXTERN	;GO TO IT
                        
                        * RUN EXECUTIVE
                        
0757 : 4f               RUNEXC		clra
0758 : 9712             		staa	CRFLAG
075a : 971c             		staa	LETFLG
075c : 9718             		staa	DIMFLG
075e : 972c             		staa	STKCNT
0760 : 9619             		lda	RUNFLG	;RUN MODE?
0762 : 2603             		bne	RUNEX0
0764 : 7e0203           RUNEXA		jmp	FILBUF
0767 : de04             RUNEX0		ldx	BUFPNT	;SET POINTER
0769 : 860d             RUNE05		lda	#CR
076b : c63a             		ldab	#':'	;SETUP TERMINATORS
076d : a100             RUNEX1		cmpa	0,x	;C.R. ?
076f : 2707             		beq	RUNEX2
0771 : e100             		cmpb	0,x	;IS IT A ':' ?
0773 : 270a             		beq	RUNE27
0775 : 08               		inx		;BUMP THE POINTER
0776 : 20f5             		bra	RUNEX1	;REPEAT
0778 : 08               RUNEX2		inx
0779 : bc0d9d           RUNE22		cpx	ENDSTR	;END OF STORAGE?
077c : 27e6             		beq	RUNEXA
077e : 08               RUNE25		inx		;BUMP THE POINTER
077f : 08               RUNE27		inx
0780 : bd010c           		jsr	BREAK	;GO CHECK BREAK
0783 : bd03cf           RUNEX3		jsr	FNDKEY	;FIND KEY WORD
0786 : 4d               		tsta
0787 : 260b             		bne	RUNEX4
0789 : de04             		ldx	BUFPNT	;SET POINTER
078b : 8d0b             		bsr	TSTLET
078d : 2705             		beq	RUNEX4
078f : 8610             		lda	#$10
0791 : 7e04b7           RUNE35		jmp	MISTAK
0794 : ee00             RUNEX4		ldx	0,x
0796 : 6e00             		jmp	0,x	;GO TO ROUTINE
                        
                        * TEST FOR IMPLIED LET
                        
0798 : bd0d33           TSTLET		jsr	CLASS	;CHECK CHAR.
079b : c102             		cmpb	#2	;LETTER?
079d : 2612             		bne	TSTLE2
079f : 08               		inx		;BUMP THE POINTER
07a0 : bd03bc           		jsr	SKIPSP	;SKIP SPACES
AS02 Assembler for M6802 [1.42].                                     Page   21
-------------------------------- MicroBas.asm --------------------------------

07a3 : 813d             		cmpa	#'='	;EQUALS?
07a5 : 2704             		beq	TSTLE1
07a7 : 8128             		cmpa	#'('	;LEFT PARENT
07a9 : 2606             		bne	TSTLE2
07ab : ce0123           TSTLE1		ldx	#LETADR	;SET POINTER
07ae : 971c             		sta	LETFLG	;SET FLAG
07b0 : 5f               		clrb
07b1 : 39               TSTLE2		rts
                        
                        * RUN ROUTINE
                        
07b2 : bd018b           RUN		jsr	CLRBEG
07b5 : bd0195           		jsr	CLREND
07b8 : fe010f           		ldx	MEMEND
07bb : df06             		stx	FORSTK
07bd : ce1000           		ldx	#STORSP	;SET POINTER
07c0 : 7c0019           		inc	RUNFLG
07c3 : 20b4             		bra	RUNE22
                        
                        * LET ROUTINE
                        
07c5 : de04             LET		ldx	BUFPNT
07c7 : 961c             		lda	LETFLG	;TEST FLAG
07c9 : 2603             		bne	LET2
07cb : bd03ad           		jsr	NXTBLK	;FIND NEXT
07ce : bd09b6           LET2		jsr	EXPEQU
                        ;	noopt
07d1 : 2084             		jmp	RUNEXC
                        ;	opt
                        
                        * GOTO ROUTINE
                        
07d3 : bd03c3           GOTO		jsr	NXTSPC	;FIND BLOCK
07d6 : bd0a77           GOTO1		jsr	EXPR	;GO EVALUATE
07d9 : bd02f9           GOTO2		jsr	FNDLIN	;GO FIND LINE
07dc : 5d               GOTO3		tstb		;FIND?
07dd : 2705             		beq	GOTO5
07df : 8616             		lda	#$16	;SET ERROR
07e1 : 7e04b7           GOTO4		jmp	MISTAK	;REPORT
07e4 : 5c               GOTO5		incb
07e5 : d719             		stab	RUNFLG	;SET RUN FLAG
                        ;	noopt
07e7 : 2090             		jmp	RUNE22
                        ;	opt
                        
                        * INPUT ROUTINE
                        
07e9 : bd03c3           INPUT		jsr	NXTSPC	;FIND NEXT
07ec : 7f0013           INPUT0		clr	QMFLAG	;CLEAR FLAG
07ef : bd03bc           INPUT1		jsr	SKIPSP	;SKIP SPACES
07f2 : 8122             		cmpa	#'"'	;IS IT A QUOTE?
07f4 : 2606             		bne	INPUT2
07f6 : 08               		inx		;BUMP THE POINTER
07f7 : bd059a           		jsr	PSTRNG	;OUTPUT STRING
07fa : 203b             		bra	INPUT6
07fc : bd05cf           INPUT2		jsr	FNDLBL	;FIND LABLE
07ff : df33             		stx	XTEMP4	;SAVE POINTER
0801 : ce0068           INPUT3		ldx	#BUFFER	;SET POINTER
0804 : 9613             		lda	QMFLAG	;TEST FLAG
0806 : 2607             		bne	INPUT4
0808 : 863f             		lda	#'?'
080a : 9713             		sta	QMFLAG	;SET FLAG
AS02 Assembler for M6802 [1.42].                                     Page   22
-------------------------------- MicroBas.asm --------------------------------

080c : bd049e           		jsr	OUTCH	;OUT A ?
080f : bd0109           INPUT4		jsr	INCH	;GET A DIGIT
0812 : 8118             		cmpa	#DELCOD	;DELETE?
0814 : 2605             		bne	INPU45
0816 : 7f0013           		clr	QMFLAG
0819 : 20e6             		bra	INPUT3
081b : a700             INPU45		sta	0,x	;SAVE IT
081d : 08               		inx
081e : 812c             		cmpa	#','	;1S IT COMMA?
0820 : 2709             		beq	INPUT5
0822 : 810d             		cmpa	#$D	;IS IT A C.R.?
0824 : 26e9             		bne	INPUT4
0826 : 9712             		staa	CRFLAG	;SET FLAG
0828 : bd033e           		jsr	PCRLF	;OUTPUT A CR & LF
082b : ce0068           INPUT5		ldx	#BUFFER	;SET POINTER
082e : bd036e           		jsr	BCDCON	;GO CNVRT NUM.
0831 : de33             		ldx	XTEMP4
0833 : 8d2d             		bsr	LABLS2
0835 : df04             		stx	BUFPNT	;SAVE POINTER
0837 : 812c             INPUT6		cmpa	#','	;IS IT A COMMA?
0839 : 2607             		bne	INPUT7
083b : 08               		inx
083c : 9612             		lda	CRFLAG	;TEST FLAG
083e : 27af             		beq	INPUT1
0840 : 20aa             		bra	INPUT0
0842 : bd035c           INPUT7		jsr	TSTTRM
0845 : 2613             		bne	INPUT9
0847 : 9612             INPU72		lda	CRFLAG	;TEST FLAG
0849 : 2703             		beq	INPUT8
084b : 7e0757           INPU75		jmp	RUNEXC
084e : bd0109           INPUT8		jsr	INCH	;GET CHAR.
0851 : 810d             		cmpa	#$D	;C.R.?
0853 : 26f9             		bne	INPUT8
0855 : bd033e           		jsr	PCRLF
0858 : 20f1             		bra	INPU75
085a : 8645             INPUT9		lda	#$45
085c : 7e04b7           		jmp	MISTAK	;REPORT ERROR
                        
                        
                        * GET AND PUT LABLE
                        
085f : bd05cf           LABLES		jsr	FNDLBL	;GO FIND IT
0862 : bd06b7           LABLS2		jsr	PUTLBL	;GO PUT IT
0865 : 7e03c3           		jmp	NXTSPC	;GET TO NEXT SET
                        
                        
                        * DATA ROUTINE
                        
0868 : 9619             DATA		lda	RUNFLG	;RUNNING?
086a : 2749             		beq	READ6
086c : bd03c3           		jsr	NXTSPC	;FIND NEXT
086f : 971a             		staa	DATAFL	;SET DATA FLAG
0871 : df0c             		stx	DATAST	;SET POINTER
0873 : df0e             		stx	DATAPT
0875 : 203e             		bra	READ6	;RETURN
                        
                        
                        * READ DATA ROUTINE
                        
0877 : 9619             READ		lda	RUNFLG	;RUNNING?
0879 : 273a             		beq	READ6
087b : 961a             		lda	DATAFL	;CHECK FLAG
AS02 Assembler for M6802 [1.42].                                     Page   23
-------------------------------- MicroBas.asm --------------------------------

087d : 2739             		beq	READ8
087f : bd03ad           		jsr	NXTBLK	;GET NEXT
0882 : bd03bc           READ2		jsr	SKIPSP	;GO CLASSIFY
0885 : bd05cf           		jsr	FNDLBL
0888 : df33             		stx	XTEMP4
088a : de04             		ldx	BUFPNT
088c : df35             		stx	XTEMP5	;SAVE IT
088e : de0e             		ldx	DATAPT	;GET DATA PNTR
0890 : bd0a77           		jsr	EXPR	;GET DATA
0893 : a600             		lda	0,x	;GET CHAR.
0895 : bd035c           		jsr	TSTTRM	;TEST IT
0898 : 2604             		bne	READ25
089a : de0c             		ldx	DATAST	;SET POINTER
089c : 2001             		bra	READ3
089e : 08               READ25		inx		;BUMP THE POINTER
089f : df0e             READ3		stx	DATAPT
08a1 : de35             		ldx	XTEMP5
08a3 : df04             		stx	BUFPNT
08a5 : de33             		ldx	XTEMP4
08a7 : 8db9             		bsr	LABLS2
08a9 : 812c             		cmpa	#','	;IS IT A COMMA?
08ab : 2603             		bne	READ4
08ad : 08               		inx
08ae : 20d2             		bra	READ2	;REPEAT
08b0 : bd035c           READ4		jsr	TSTTRM
08b3 : 2603             		bne	READ8	;ERROR
08b5 : 7e0757           READ6		jmp	RUNEXC	;RETURN
08b8 : 8651             READ8		lda	#$51
08ba : 7e04b7           		jmp	MISTAK
                        
                        * RESTORE DATA STRING
                        
08bd : df22             RESTOR		stx	XSAVE	;SAVE POINTER
08bf : de0c             		ldx	DATAST
08c1 : df0e             		stx	DATAPT	;FIX DATA PNTR
08c3 : de22             		ldx	XSAVE	;RESTORE POINTER
08c5 : 20ee             		bra	READ6
                        
                        * ON GOTO ROUTINE
08c7 : bd03ad           ONGOTO		jsr	NXTBLK	;FIND NEXT BLOCK
08ca : bd0a77           		jsr	EXPR	;EVAL. EXPR.
08cd : 9664             		lda	NUMBER+2
08cf : 840f             		anda	#$0F	;MASK L.S. DIGIT
08d1 : 36               		psha		;SAVE A
08d2 : 7f0012           		clr	CRFLAG
08d5 : 08               		inx		;BUMP THE POINTER
08d6 : 08               		inx
08d7 : a600             		lda	0,x	;GET CHAR
08d9 : 8154             		cmpa	#'T'	;IS IT A "T"?
08db : 2702             		beq	ONGOT0
08dd : 9712             		staa	CRFLAG	;SET FLAG
08df : bd03af           ONGOT0		jsr	NXTBL4	;GET NEXT
08e2 : df22             		stx	XSAVE	;SAVE X
08e4 : 32               		pula		;RESTORE A
08e5 : 4a               ONGOT1		deca
08e6 : 2711             		beq	ONGOT4
08e8 : e600             ONGOT2		ldab	0,x	;GET A CHAR,
08ea : 08               		inx	;BUMP THE POINTER
08eb : c12c             		cmpb	#','	;IS IT A COMMA?
08ed : 2604             		bne	ONGOT3
08ef : df22             		stx	XSAVE	;SAVE THE POINTER
08f1 : 20f2             		bra	ONGOT1	;REPEAT
AS02 Assembler for M6802 [1.42].                                     Page   24
-------------------------------- MicroBas.asm --------------------------------

08f3 : c10d             ONGOT3		cmpb	#CR	;C^R^ ?
08f5 : 26f1             		bne	ONGOT2
08f7 : de22             		ldx	XSAVE	;RESTORE POINTER
08f9 : d612             ONGOT4		ldab	CRFLAG	;CHECK FLAG
08fb : 2703             		beq	ONGOT6
08fd : 7e0983           		jmp	GOSUB2
0900 : 7e07d6           ONGOT6		jmp	GOTO1
                        
                        * ROUTINE
                        
0903 : bd03c3           IFS		jsr	NXTSPC	;FIND NEXT
0906 : bd0a77           		jsr	EXPR	;EUAL EXPR
0909 : a600             		lda	0,x	;GET CHAR
090b : 8d63             		bsr	CLSREL	;REL OPERATOR?
090d : 265c             		bne	IF9	;ERROR!
090f : 36               		psha		;SAVE A
0910 : a601             		lda	1,x	;GET CHAR
0912 : 8d5c             		bsr	CLSREL	;REL OP?
0914 : 32               		pula		;RESTORE A
0915 : 2604             		bne	IF1
0917 : e601             		ldab	1,x
0919 : 1b               		aba		;FORM REL CODE
091a : 08               		inx		;BUMP THE POINTER
091b : 08               IF1		inx
091c : 36               		psha		;SAVE A
091d : bd0a77           		jsr	EXPR	;EVAL EXPR
0920 : 32               		pula
0921 : 840f             		anda	#$0F	;MASK
0923 : 8009             		suba	#9	;BIAS IT
0925 : 2b44             		bmi	IF9	;ERROR?
0927 : 48               		asla		;TIMES FOUR
0928 : 48               		asla
0929 : b70933           		staa	OFSET3+1
092c : bd0c15           		jsr	SUB	;GO COMPARE
092f : bd0d0e           		jsr	ZCHK	;SET CC REG
0932 : 20fe             OFSET3		bra	*
0934 : 2f18             BRATBL		ble	IF4	;BRANCH TABLE
0936 : 2030             		bra	IF8
0938 : 2614             		bne	IF4
093a : 202c             		bra	IF8
093c : 2c10             		bge	IF4
093e : 2028             		bra	IF8
0940 : 2d0c             		blt	IF4
0942 : 2024             		bra	IF8
0944 : 2708             		beq	IF4
0946 : 2020             		bra	IF8
0948 : 2e04             		bgt	IF4
094a : 201c             		bra	IF8
094c : 201d             		bra	IF9	;ERROR!
094e : de04             IF4		ldx	BUFPNT	;SET POINTER
0950 : a600             		lda	0,x	;GET CHAR
0952 : 8154             		cmpa	#'T'	;IS IT A "T"?
0954 : 260f             		bne	IF6
0956 : bd03c3           		jsr	NXTSPC
0959 : df04             		stx	BUFPNT	;SAVE POINTER
095b : bd0d35           		jsr	CLASS1	;GO CLASSIFY
095e : c103             		cmpb	#3	;IS IT A NUMBER?
0960 : 2603             		bne	IF6
0962 : 7e07d6           		jmp	GOTO1	;GO TO GOTO
0965 : 7e0783           IF6		jmp	RUNEX3
0968 : 7e0757           IF8		jmp	RUNEXC	;GO PROCESS CMND
096b : 8662             IF9		lda	#$62	;SET ERROR
AS02 Assembler for M6802 [1.42].                                     Page   25
-------------------------------- MicroBas.asm --------------------------------

096d : 7e04b7           		jmp	MISTAK
                        
                        * CLASSIFY RELATIONAL OPERATION
                        
0970 : 813b             CLSREL		cmpa	#$3B
0972 : 2306             		bls	CLSRE5
0974 : 813e             		cmpa	#$3E	;CHECK CHAR
0976 : 2202             		bhi	CLSRE5
0978 : 5f               		clrb		;CLEAR FLAG
0979 : 39               		rts		;RETURN
097a : 5c               CLSRE5		incb		;SET FLAG
097b : 39               		rts		;RETURN
                        
                        * GOSUB ROUTINE
                        
097c : d619             GOSUB		ldab	RUNFLG
097e : 27e8             		beq	IF8
0980 : bd03c3           		jsr	NXTSPC	;FIND NEXT
0983 : 7c001b           GOSUB2		inc	SUBCNT
0986 : bd0a77           		jsr	EXPR	;EVALUATE EXPR
0989 : 09               		dex
098a : bd031a           		jsr	FNDCRT	;FIND C.R.
098d : 08               		inx		;BUMP THE POINTER
098e : a600             		lda	0,x	;GET LINE NO
0990 : 36               		psha
0991 : a601             		lda	1,x
0993 : 36               		psha		;SAVE AS RET. ADD.
0994 : 9f37             		sts	CPX1	;SAVE SP
0996 : cea023           		ldx	#STKBOT+35
0999 : bd0d01           		jsr	CMPX	;CHECK BOUNDS
099c : 2303             		bls	GOSUB4
099e : 7e02f4           		jmp	ADJEN2	;RPT OVFL
09a1 : 7e07d9           GOSUB4		jmp	GOTO2
                        
                        * RETURN ROUTINE
                        
09a4 : 8673             RETURN		lda	#$73
09a6 : 7a001b           		dec	SUBCNT	;DEC COUNTER
09a9 : 2a03             		bpl	RETUR2
09ab : 7e04b7           		jmp	MISTAK	;ERROR!
09ae : 32               RETUR2		pula		;GET RET. ADD.
09af : 33               		pulb
09b0 : bd02fd           		jsr	FINDLN	;GO FIND LINE
09b3 : 7e07dc           		jmp	GOTO3
                        
                        * EXPRESSION EQUATE
                        
09b6 : bd05cd           EXPEQU		jsr	FNDLB0	;FIND LABLE
09b9 : df33             		stx	XTEMP4	;SAVE
09bb : bd03c3           		jsr	NXTSPC
09be : 08               		inx
09bf : bd0a77           		jsr	EXPR	;GO EVALUATE
09c2 : de33             		ldx	XTEMP4	;GET POINTER
09c4 : 7e06b7           		jmp	PUTLBL	;INSTALL
                        
                        * FOR ROUTINE
                        
09c7 : bd03ad           FOR		jsr	NXTBLK	;FIND NEXT
09ca : 36               		psha
09cb : 8de9             		bsr	EXPEQU
09cd : de08             		ldx	DIMPNT
09cf : df37             		stx	CPX1
AS02 Assembler for M6802 [1.42].                                     Page   26
-------------------------------- MicroBas.asm --------------------------------

09d1 : de06             		ldx	FORSTK
09d3 : 32               		pula
09d4 : a700             		staa	0,x
09d6 : 9605             		lda	BUFPNT+1
09d8 : 09               		dex		;DEC THE POINTER
09d9 : a700             		staa	0,x
09db : 9604             		lda	BUFPNT	;SET UP INDEX
09dd : 09               		dex
09de : a700             		sta	0,x
09e0 : 09               		dex
09e1 : bd0d01           		jsr	CMPX	;CHECK FOR OVFLW
09e4 : 2203             		bhi	FOR5
09e6 : 7e02f4           		jmp	ADJEN2
09e9 : df06             FOR5		stx	FORSTK	;SAVE POINTER
09eb : 7e0757           		jmp	RUNEXC
                        
                        * NEXT ROUTINE
                        
09ee : bd03ad           NEXT		jsr	NXTBLK	;FIND NEXT
09f1 : df1e             		stx	NXPNTR
09f3 : de06             		ldx	FORSTK	;SET POINTER
09f5 : bc010f           NEXT1		cpx	MEMEND	;OVFLW?
09f8 : 2604             		bne	NEXT2
09fa : de04             		ldx	BUFPNT	;RESTORE PNTR
09fc : 2074             		bra	NEXT9	;ERROR!
09fe : 08               NEXT2		inx		;FIXUP POINTER
09ff : 08               		inx
0a00 : 08               		inx
0a01 : a100             		cmpa	0,x	;CHECK
0a03 : 26f0             		bne	NEXT1
0a05 : 09               		dex		;FIX POINTER
0a06 : 09               		dex
0a07 : 09               		dex
0a08 : df06             		stx	FORSTK
0a0a : 08               		inx
0a0b : ee00             		ldx	0,x
0a0d : df04             		stx	BUFPNT	;SAVE IT
0a0f : bd05cf           		jsr	FNDLBL	;FIND LABLE
0a12 : df33             		stx	XTEMP4	;SAVE IT
0a14 : bd03c3           		jsr	NXTSPC	;FIND NEXT
0a17 : bd0a77           		jsr	EXPR	;EVALUATE
0a1a : bd0ba2           		jsr	STAKUP
0a1d : de33             		ldx	XTEMP4	;RESTORE PNTR
0a1f : bd0b95           		jsr	GETVAL	;GET LABLE VALUE
0a22 : de04             		ldx	BUFPNT
0a24 : a600             		lda	0,x	;GET CHAR
0a26 : 8153             		cmpa	#'S'	;IS IT STEP?
0a28 : 2708             		beq	NEXT4
0a2a : bd0363           		jsr	UPSCLR
0a2d : 4c               		inca
0a2e : 9764             		sta	NUMBER+2
0a30 : 200a             		bra	NEXT5
0a32 : bd03c5           NEXT4		jsr	NXTSP4
0a35 : bd0a77           		jsr	EXPR
0a38 : 9662             		lda	NUMBER
0a3a : 971c             		staa	LETFLG	;SHOW NEG.
0a3c : bd0c1b           NEXT5		jsr	ADD	;GO ADD IN STEP
0a3f : ce0010           		ldx	#TRYVAL	;SET POINTER
0a42 : bd06b7           		jsr	PUTLBL	;SAVE LABLE
0a45 : bd0c15           		jsr	SUB	;COMPARE
0a48 : bd0d0e           		jsr	ZCHK	;SET CC REG
0a4b : d61c             		ldab	LETFLG	;CHK FLAG
AS02 Assembler for M6802 [1.42].                                     Page   27
-------------------------------- MicroBas.asm --------------------------------

0a4d : 2b05             		bmi	NEXT6
0a4f : 06               		tap		;SET CC
0a50 : 2c12             		bge	NEXT8
0a52 : 2003             		bra	NEXT7
0a54 : 06               NEXT6		tap		;SET CC
0a55 : 2f0d             		ble	NEXT8
0a57 : de06             NEXT7		ldx	FORSTK
0a59 : 08               		inx		;FIXUP PNTR
0a5a : 08               		inx
0a5b : 08               		inx
0a5c : df06             		stx	FORSTK	;SAVE IT
0a5e : de1e             		ldx	NXPNTR
0a60 : df04             		stx	BUFPNT	;SAVE
0a62 : 200b             		bra	NEXT85
0a64 : ce0010           NEXT8		ldx	#TRYVAL
0a67 : bd0b95           		jsr	GETVAL
0a6a : de33             		ldx	XTEMP4
0a6c : bd06b7           		jsr	PUTLBL
0a6f : 7e0757           NEXT85		jmp	RUNEXC
0a72 : 8681             NEXT9		lda	#$81	;SET ERROR
0a74 : 7e04b7           NEXTIO		jmp	MISTAK
                        
                        * EXPRESSION HANDLER
                        
0a77 : 7f002c           EXPR		clr	STKCNT	;SET COUNT = 0
0a7a : 962c             EXPRO		lda	STKCNT
0a7c : 972d             		sta	AUXCNT
0a7e : 8d04             		bsr	EVAL
0a80 : 4d               		tsta		;CHECK FOR ERROR
0a81 : 26f1             		bne	NEXTIO
0a83 : 39               EXPR1		rts		;RETURN
                        *
                        **EVAL
                        * EVALUATE AN ALGEBRAIC STRING
                        *
0a84 : 9ffe             EVAL		sts	STKTOP	;SAVE SP TOP
0a86 : bd0d2e           EVA0A		jsr	SKYCLS
0a89 : df04             		stx	BUFPNT
0a8b : c101             		cmpb	#1	;SEE IF EMPTY EXPRESSION
0a8d : 2604             		bne	EVAL0
0a8f : 8621             		lda	#$21
0a91 : 204a             		bra	EVAL3
0a93 : 54               EVAL0		lsrb		;SET UP
0a94 : c103             		cmpb	#3	;CHECK FOR UNARY + OR -
0a96 : 2603             		bne	EVAL1
0a98 : bd0363           		jsr	UPSCLR
0a9b : de04             EVAL1		ldx	BUFPNT
0a9d : bd0d2e           EVAL1A		jsr	SKYCLS	;GET NEXT CHAR
0aa0 : df04             		stx	BUFPNT
0aa2 : c104             		cmpb	#4	;CHECK FOR OPERATORS
0aa4 : 2302             		bls	EVAL1Z
0aa6 : c605             		ldab	#5	;SET UP
0aa8 : 58               EVAL1Z		aslb
0aa9 : f70aad           		stab	OFFREL+1	;SET UP BRANCH
0aac : 20fe             OFFREL		bra	*
0aae : 202b             		bra	EVAL2	;ERROR
0ab0 : 201b             		bra	EVAL4	;TERMINATOR
0ab2 : 2038             		bra	EVAL8	;LETTER
0ab4 : 202c             		bra	EVAL7	;NUMBER
0ab6 : 2004             		bra	EVAL1C	;RIGHT PAREN
0ab8 : 36               		psha	;SAVE
0ab9 : 08               		inx
AS02 Assembler for M6802 [1.42].                                     Page   28
-------------------------------- MicroBas.asm --------------------------------

0aba : 20ca             		bra	EVA0A	;AGAIN
0abc : 30               EVAL1C		tsx		;GET SP
0abd : 09               		dex		;ADJUST
0abe : d618             		ldab	DIMFLG
0ac0 : 9cfe             		cpx	STKTOP	;CHECK FOR EMPTY
0ac2 : 2706             		beq	EVAL1E
0ac4 : 32               		pula
0ac5 : 5f               		clrb
0ac6 : 8128             		cmpa	#'('	;CHECK FOR L PAREN ON STACK
0ac8 : 27f2             		beq	EVAL1C	;IF SO, OK
0aca : 5d               EVAL1E		tstb		;CHECK FOR ALRIGHT
0acb : 270e             		beq	EVAL2	;IF NOT SET, ERROR
0acd : 4f               EVAL4		clra
0ace : d62c             		ldab	STKCNT	;GET STACK STKCNT
0ad0 : 5a               		decb		;CHECK OP STACK
0ad1 : d12d             		cmpb	AUXCNT
0ad3 : 2606             		bne	EVAL2	;IF NOT EMPTY, ERROR
0ad5 : 30               		tsx
0ad6 : 09               		dex		;ALIGN
0ad7 : 9cfe             		cpx	STKTOP	;CHECK OPERATOR STACK
0ad9 : 2704             		beq	EVAL3A	;IF NOT EMPTY ERROR
0adb : 8620             EVAL2		lda	#$20	;SET ERROR NUMBER
0add : 9efe             EVAL3		lds	STKTOP	;GET SP
0adf : de04             EVAL3A		ldx	BUFPNT	;SET POINTER
0ae1 : 39               		rts
0ae2 : bd0ba2           EVAL7		jsr	STAKUP	;SHIFT OP STACK UP
0ae5 : de04             		ldx	BUFPNT
0ae7 : bd036e           		jsr	BCDCON	;GET OPERAND
0aea : 2059             		bra	EVAL12
0aec : a601             EVAL8		lda	1,x	;GET NEXT CHAR
0aee : bd0d35           		jsr	CLASS1	;GO CLASSIFY
0af1 : c102             		cmpb	#2	;CHECK FOR LETTER
0af3 : 2628             		bne	EVAL9	;IF NOT, VARIABLE
0af5 : a600             		lda	0,x	;GET CHAR BACK
0af7 : df22             		stx	XSAVE	;SET FOR ENTRY TO FIMDKEY
0af9 : ce017b           		ldx	#FCTTBL
0afc : bd03d8           		jsr	FNDKE2	;GO CHECK FUNCTION
0aff : 4d               		tsta		;CHECK SUCCESS
0b00 : 27cb             		beq	EVAL4
0b02 : 7e0794           		jmp	RUNEX4	;GO SERVICE
0b05 : 863f             EVAL86		lda	#'?'	;GET STGNUM OPERATOR
0b07 : 36               EVAL87		psha		;PUT ON STACK
0b08 : de22             		ldx	XSAVE
0b0a : 7e0a86           		jmp	EVA0A
0b0d : 8640             EVAL85		lda	#'@'	;GET ABS OPERATOR
0b0f : 20f6             		bra	EVAL87
0b11 : bd0363           EVAL88		jsr	UPSCLR	;MOVE STACK UP
0b14 : bd0d7a           		jsr	RANDOM	;COMPUTE RANDOM #
0b17 : 9764             		sta	NUMBER+2
0b19 : de22             EVAL89		ldx	XSAVE	;RESTORE POINTER
0b1b : 2028             		bra	EVAL12
0b1d : d6fe             EVAL9		ldab	STKTOP
0b1f : 37               		pshb
0b20 : d6ff             		ldab	STKTOP+1
0b22 : 37               		pshb
0b23 : d62d             		ldab	AUXCNT	;GET COUNTER
0b25 : 37               		pshb		;SAVE
0b26 : d618             		ldab	DIMFLG	;GET FLAG
0b28 : 37               		pshb		;SAVE
0b29 : bd05cd           		jsr	FNDLB0	;FIND VARIABLE STORAGE
0b2c : 33               		pulb		;GET FLAG
0b2d : d718             		stab	DIMFLG	;RESTORE
AS02 Assembler for M6802 [1.42].                                     Page   29
-------------------------------- MicroBas.asm --------------------------------

0b2f : 33               		pulb		;GET COUNTER
0b30 : d72d             		stab	AUXCNT	;RESTORE
0b32 : 33               		pulb
0b33 : d7ff             		stab	STKTOP+1
0b35 : 33               		pulb
0b36 : d7fe             		stab	STKTOP
0b38 : bd0ba2           		jsr	STAKUP
0b3b : de20             		ldx	XTEMP
0b3d : bd0b95           		jsr	GETVAL	;MOVE VALUE TO NUMBER
0b40 : 2005             		bra	EVA12A
0b42 : de04             EVA11C		ldx	BUFPNT	;RESTORE POINTER
0b44 : 08               		inx
0b45 : df04             EVAL12		stx	BUFPNT	;SAVE POINTER
0b47 : 30               EVA12A		tsx
0b48 : 09               		dex
0b49 : 9cfe             		cpx	STKTOP	;CHECK OPERATOR STACK
0b4b : 2737             		beq	EVAL10	;IF EMPTY, DON'T OPERATE
0b4d : 32               		pula
0b4e : 36               		psha		;PUT BACK
0b4f : 8128             		cmpa	#'('	;CHECK FOR LEFT PAREM
0b51 : 2731             		beq	EVAL10	;IF SO, DON'T OPERATE
0b53 : bd0d35           		jsr	CLASS1	;GO CLASSYFY
0b56 : 37               		pshb
0b57 : 54               		lsrb		;SET UP ID
0b58 : 962c             		lda	STKCNT	;GET COUNT
0b5a : 4a               		deca
0b5b : c104             		cmpb	#4 	;FOR ABS OR SON
0b5d : 2704             		beq	EVA12C	;IF SO, GO AHEAD
0b5f : 912d             		cmpa	AUXCNT	;OTHERWISE CHECK FOR 2 OPERANDS
0b61 : 2721             		beq	EVAL10	;IF NOT, ABORT
0b63 : 8109             EVA12C		cmpa	#9	;CHECK OVERFLOW
0b65 : 2304             		bls	EVA12D	;OK
0b67 : 8624             		lda	#$24	;SET ERROR
0b69 : 2016             		bra	EVAL19
0b6b : 32               EVA12D		pula		;GET CLASSIFICATION
0b6c : 33               		pulb		; GET OPERATOR
0b6d : 8006             		suba	#6	;REMOVE BIAS
0b6f : 48               		asla		;#2
0b70 : b70b77           		sta	OPOFF+1	;SET UP JMP
0b73 : ce0b87           		ldx	#OPTBL	;POINT
0b76 : ee00             OPOFF		ldx	0,x
0b78 : ad00             		jsr	0,x	;GO OPERATE
0b7a : bd0d0e           		jsr	ZCHK	;CHECK RESULT
0b7d : 28c8             		bvc	EVA12A	;IF NO OVFL, GO OPERATE AGAIN
0b7f : 8623             EVAL18		lda	#$23	;SET ERROR NUMBER
0b81 : 7e0add           EVAL19		jmp	EVAL3
0b84 : 7e0a9b           EVAL10		jmp	EVAL1
0b87 : 0c1b             OPTBL		fdb	ADD
0b89 : 0c15             		fdb	SUB
0b8b : 0cd2             		fdb	SIGNUM
0b8d : 0c0d             		fdb	ABSVAL
0b8f : 0c45             		fdb	MULT
0b91 : 0c66             		fdb	DIVIDE
0b93 : 0ce4             		fdb	EXPON
                        *
                        ** GET VALUE
                        * MOVE 3 BYTES POINTED TO BY X TO NUMBER
                        *
0b95 : a600             GETVAL		lda	0,x	;GET VALUE
0b97 : 9762             		sta	NUMBER	;STORE
0b99 : a601             		lda	1,x
0b9b : 9763             		sta	NUMBER+1
AS02 Assembler for M6802 [1.42].                                     Page   30
-------------------------------- MicroBas.asm --------------------------------

0b9d : a602             		lda	2,x
0b9f : 9764             		sta	NUMBER+2
0ba1 : 39               		rts
                        *
                        *
                        ** STACKUP
                        * ROLL OPERATIONAL STACK UPWARD
                        *
0ba2 : ce003b           STAKUP		ldx	#STKEND	;POINT TO END
0ba5 : e603             STAKU2		ldab	3,x
0ba7 : e700             		stab	0,x	;MOVE
0ba9 : 08               		inx
0baa : 8c0062           		cpx	#NUMBER	;SEE IF DONE
0bad : 26f6             		bne	STAKU2
0baf : 7c002c           		inc	STKCNT
0bb2 : 39               		rts
                        *
                        *
                        ** STACKDOWN
                        * ROLL OPERATIONAL STACK DOWNWARD
                        *
0bb3 : ce0064           STAKDN		ldx	#AX-1	;POINT TO STORE
0bb6 : e600             STAKD1		ldab	0,x
0bb8 : e703             		stab	3,x
0bba : 09               		dex
0bbb : 8c003a           		cpx	#STKEND-1	;SEE IF DONE
0bbe : 26f6             		bne	STAKD1
0bc0 : 7a002c           		dec	STKCNT
0bc3 : 39               		rts
                        *
                        *
                        ** UADD
                        * UNSIGNED ADD OF AX TO NUMBER
                        *
0bc4 : 0c               UADD		clc		;ZERO THE CARRY
0bc5 : ce0064           UADD1		ldx	#NUMBER+2	;POINT TO STORE
0bc8 : a600             UADD2		lda	0,x	;GET ADDEND
0bca : a903             		adca	3,x	;ADD IN AUGEND
0bcc : 19               		daa
0bcd : a700             		staa	0,x	;SAVE
0bcf : 09               		dex
0bd0 : 8c0061           		cpx	#NUMBER-1	;SEE IF DONE
0bd3 : 26f3             		bne	UADD2
0bd5 : 37               UADD22		pshb
0bd6 : c602             		ldab	#$02	;SET FOR OVFL
0bd8 : 85f0             		bita	#$F0	;AND AGAIN
0bda : 2601             		bne	UADD25
0bdc : 5f               		clrb		;RESET OFVL
0bdd : da30             UADD25		orab	OVFLBF
0bdf : d730             		stab	OVFLBF	;SET OVFL IF NECESSARY
0be1 : 17               		tba
0be2 : 33               		pulb
0be3 : 39               UADD3		rts
                        *
                        *
                        **USUB
                        * UNSIGNED SUBTRACT OF AX FROM NUMBER
                        *
0be4 : 8d03             USUB		bsr	TENCOM	;GO TEN'S COMPLEMENT
0be6 : 0d               		sec		;FIX UP
0be7 : 20dc             		bra	UADD1	;GO ADD
                        *
AS02 Assembler for M6802 [1.42].                                     Page   31
-------------------------------- MicroBas.asm --------------------------------

                        *
                        **TENCOM
                        * UNSIGNED TEN'S COMPLEMENT OF AX (ALMOST)
                        *
0be9 : ce0067           TENCOM		ldx	#AX+2	;POINT TO AX
0bec : 8699             TENCO1		lda	#$99
0bee : a000             		suba	0,x	;SUBTRACT FROM 99
0bf0 : a700             		staa	0,x	;SAVE
0bf2 : 09               		dex
0bf3 : 8c0064           		cpx	#AX-1
0bf6 : 26f4             		bne	TENCO1
0bf8 : 840f             		anda	#$0F	;RESET SIGN
0bfa : a701             		staa	1,x	;STORE
0bfc : 39               		rts
                        *
                        *
                        ** SET SIN
                        * CALCULATE RESULT SIGN
                        *
0bfd : 7f0030           SETSIN		clr	OVFLBF	;CLEAR OVFL INDICATOR
0c00 : 9665             SETSI0		lda	AX	;GET SIGN
0c02 : 16               		tab		;SAVE
0c03 : c40f             		andb	#$0F	;RESET SIGN
0c05 : d765             		stab	AX	;PUT BACK
0c07 : 972f             		staa	AXSIGN	;SAVE SIGN
0c09 : 9862             		eora	NUMBER	;FORM NEW SIGN
0c0b : 972e             		staa	SIGN	;SAVE
0c0d : d662             ABSVAL		ldab	NUMBER	;GET MS BYTE
0c0f : c40f             		andb	#$0F	;RESET SIGN
0c11 : d762             		stab	NUMBER	;PUT BACK
0c13 : 4d               		tsta		;TEST NEW SIGN
0c14 : 39               		rts
                        *
                        *
                        **
                        * SUBTRACT AX FROM NUMBER
                        *
0c15 : 9662             SUB		lda	NUMBER	;GET MS BYTE
0c17 : 88f0             		eora	#$F0	;CHANGE SIGN
0c19 : 9762             		staa	NUMBER	;PUT BACK
                        * GO INTO ADD
                        *
                        *
                        * ADD
                        * ADD AX TO NUMBER
                        *
0c1b : 8d58             ADD		bsr	RELAY
0c1d : 8dde             		bsr	SETSIN	;GO CALCULATE SIGN
0c1f : 2a0a             		bpl	ADD0	;USE EITHER SIGN
0c21 : 8dc1             		bsr	USUB	;OTHERWISE SUBTRACT
0c23 : 06               		tap		; CCR
0c24 : 2809             		bvc	ADD1	;CHECK OVERFLOW
0c26 : 73002f           		com	AXSIGN	;CHANGE FOR AX SMALLER
0c29 : 200b             		bra	ADD15
0c2b : 8d97             ADD0		bsr	UADD	;GO ADD
0c2d : 200a             		bra	ADD2	;GO FIX SIGN
0c2f : 8d44             ADD1		bsr	RELAY	;COPY NUMBER TO AX
0c31 : bd0363           		jsr	UPSCLR	;RESTORE
0c34 : 8dae             		bsr	USUB	;GO NEGATE
0c36 : 7f0030           ADD15		clr	OVFLBF
0c39 : 962f             ADD2		lda	AXSIGN	;GET OLD SIGN
                        *
AS02 Assembler for M6802 [1.42].                                     Page   32
-------------------------------- MicroBas.asm --------------------------------

                        *
                        ** FIXSIN
                        * SET THE SIGN ON THE RESULT
                        *
0c3b : 84f0             FIXSIN		anda	#$F0	;MASK
0c3d : c60f             		ldab	#$0F	;SET MASK
0c3f : d462             		andb	NUMBER	;RESET SIGN
0c41 : 1b               		aba		;TACK ON SIGN
0c42 : 9762             		sta	NUMBER	;PUT BACK
0c44 : 39               FIX2		rts
                        *
                        *
                        ** MULT
                        * MULTIPLY AC BY AX
                        *
0c45 : 8d2e             MULT		bsr	RELAY	;MOVE STACK
0c47 : 8db4             		bsr	SETSIN	;GO CALC. SIGNS
0c49 : bd0363           MULT0		jsr	UPSCLR	;MOVE STACK UP
0c4c : c605             		ldab	#5	;SET COUNTER
0c4e : 965f             MULT1		lda	AC	;GET MS BYTE OF AC
0c50 : 2708             		beq	MULT3	;IF ZERO , LOOP
0c52 : bd0bc4           MULT2		jsr	UADD	;ADD IN AX
0c55 : 7a005f           		dec	AC	;ONCE DONE
0c58 : 26f8             		bne	MULT2
0c5a : 5a               MULT3 		decb		;ONCE DONE
0c5b : 273c             		beq	MULT4	;CHECK IF ALL DONE
0c5d : 8d49             		bsr	ACLEFT	;SHIFT AC LEFT
0c5f : 9662             		lda	NUMBER
0c61 : bd0bd5           		jsr	UADD22
0c64 : 20e8             		bra	MULT1
                        *
                        *
                        ** DIVIDE
                        * DIVIDE AC-NUMBER BY AX
                        *
0c66 : 8d0d             DIVIDE		bsr	RELAY
0c68 : ce0065           		ldx	#AX
0c6b : bd0d11           		jsr	ZCHK1	;GO CHECK IF AX=O
0c6e : 2608             		bne	DIVID1	;IF NOT, OK
0c70 : 8622             DIVID0		lda	#$22	;SET ERROR
0c72 : 7e0add           		jmp	EVAL3
0c75 : 7e0bb3           RELAY		jmp	STAKDN	;RELAY TO STACK DOWN
                        ;	noopt
0c78 : 8d83             DIVID1		jsr	SETSIN	;CALC, SIGNS
0c7a : bd0ba2           		jsr	STAKUP	;PUSH BACK
                        ;	opt
0c7d : 8d29             		bsr	ACLEFT	;SHIFT DOWN
0c7f : 6f02             		clr	2,x
0c81 : 6f03             		clr	3,x	;ZERO OUT NUMBER
0c83 : c605             		ldab	#5	;SET LOOP COUNT
0c85 : 8d21             DIVID2		bsr	ACLEFT	;MOVE AC DOWN
0c87 : bd0be9           DIVI2A		jsr	TENCOM	;TAKE 10'S COMP
0c8a : 8d2e             DIVID3		bsr	DADD	;GO SPECIAL ADD
0c8c : 85f0             		bita	#$F0	;CHECK FOR OVERFLOW
0c8e : 2613             		bne	DIVID4
0c90 : bd0be9           		jsr	TENCOM	;IF SO, RESTORE AX
0c93 : 0c               		clc
0c94 : 8d25             		bsr	DADD1	;ADD BACK IN
0c96 : 5a               		decb		;ONE PASS MADE
0c97 : 26ec             		bne	DIVID2
0c99 : 962e             MULT4		lda	SIGN	;GET THE SIGN
0c9b : 8d9e             		bsr	FIXSIN	;GO FIX UP THE SIGN
AS02 Assembler for M6802 [1.42].                                     Page   33
-------------------------------- MicroBas.asm --------------------------------

0c9d : ce005e           		ldx	#AC-1	;POINT TO AC
0ca0 : 7e0bb6           		jmp	STAKD1	;MOVE STACK BACK
0ca3 : 7c0064           DIVID4		inc	NUMBER+2	;ADD ONE IN
0ca6 : 20e2             		bra	DIVID3	;GO DO AGAIN
                        *
                        *
                        ** ACLEFT
                        * SHIFT AC-NUMBER LEFT 4 BITS
                        *
0ca8 : 8604             ACLEFT		lda	#4	;SET FOR 4 BITS
0caa : ce0064           ACLEF1		ldx	#AX-1	;POINT X
0cad : 0c               		clc
0cae : 6900             ACLEF2		rol	0,x	;ROTATE
0cb0 : 09               		dex
0cb1 : 8c005e           		cpx	#AC-1	;CHECK IF DONE
0cb4 : 26f8             		bne	ACLEF2
0cb6 : 4a               		deca		;CHECK FOR DONE
0cb7 : 26f1             		bne	ACLEF1
0cb9 : 39               		rts
                        *
                        *
                        ** DADD
                        * ADD AX TO A C
                        *
0cba : 0d               DADD		sec
0cbb : ce0061           DADD1		ldx	#AC+2
0cbe : 965f             		lda	AC	;GET MS BYTE
0cc0 : 840f             		anda	#$0F	;RESET SIGN
0cc2 : 975f             		staa	AC	;STORE BACK
0cc4 : a600             DADD2		lda	0,x	;GET ADDEND
0cc6 : a906             		adca	6,x	;ADD IN
0cc8 : 19               		daa
0cc9 : a700             		staa	0,x	;SAVE
0ccb : 09               		dex
0ccc : 8c005e           		cpx	#AC-1	;SEE IF DONE
0ccf : 26f3             		bne	DADD2
0cd1 : 39               		rts
                        *
                        ** SIGNUM
                        * CALCULATE SIGNUM FUNCTION
                        *
0cd2 : 8d3a             SIGNUM		bsr	ZCHK	;GO CHECK = O
0cd4 : 270b             		beq	SIGNU2	;IF SOY RESULT =0
0cd6 : d662             		ldab	NUMBER	;OTHERWISE GET SIGN
0cd8 : 8d07             SIGNU1		bsr	SIGNU2	;GO CLEAR
0cda : 7c0064           		inc	NUMBER+2	;MAKE = I
0cdd : 17               		tba		;SET FOR FIXSIN
0cde : 7e0c3b           		jmp	FIXSIN	;GO SET THE SIGN
0ce1 : 7e0366           SIGNU2		jmp	CLRNUM
                        *
                        *
                        ** EXPON
                        * CALCULATE EXPONENTIATION
                        * ONLY POSITIVE EXPONENTS UP TO 99 ALLOWED
                        *
0ce4 : 8d8f             EXPON		bsr	RELAY	;MOVE OPERANDS DOWN
0ce6 : 5f               		clrb
0ce7 : d730             		stab	OVFLBF	;CLEAR OVER FLOW
0ce9 : 9667             		lda	AX+2	;GET EXPONENT
0ceb : 27eb             		beq	SIGNU1	;IF O, GO MAKE RESULT +1
0ced : bd0ba2           		jsr	STAKUP	;GET TWO COPIES
0cf0 : 8d83             		bsr	RELAY	;MOVE DOWN
AS02 Assembler for M6802 [1.42].                                     Page   34
-------------------------------- MicroBas.asm --------------------------------

0cf2 : 8b99             EXPON1		adda	#$99	;DECREMENT
0cf4 : 19               		daa
0cf5 : 2716             		beq	CMPX2	;WHEN 0 ALL DONE
0cf7 : 36               		psha		;SAVE EXP
0cf8 : bd0c00           		jsr	SETSI0	;GO FIX SIGNS
0cfb : bd0c49           		jsr	MULT0	;GO MULTIPLY
0cfe : 32               		pula		;GET EXPONENT
0cff : 20f1             		bra	EXPON1	;LOOP
                        *
                        *
                        ** CMPX
                        * FULL COMPARE ON X
                        * COMPARES X WITH CONTENTS OF CPX1
                        *
0d01 : df39             CMPX		stx	CPX2	;SAVE
0d03 : 9639             CMPX1		lda	CPX2	;GET MS BYTE
0d05 : 9137             		cmpa	CPX1	;COMPARE
0d07 : 2604             		bne	CMPX2	;IF NOT EQUAL, DONE
0d09 : d63a             		ldab	CPX2+1	;GET LS BYTE
0d0b : d138             		cmpb	CPX1+1	;COMPARE
0d0d : 39               CMPX2		rts		;DONE
                        *
                        *
                        ** ZCHK
                        * CHECK OPERAND FOR EQUAL TO 0
                        *
0d0e : ce0062           ZCHK		ldx	#NUMBER
0d11 : 5f               ZCHK1		clrb
0d12 : 6d02             		tst	2,x
0d14 : 260e             		bne	ZCHK2
0d16 : 6d01             		tst	1,x
0d18 : 260a             		bne	ZCHK2
0d1a : a600             		lda	0,x	;GET MS BYTE
0d1c : 840f             		anda	#$0F
0d1e : 2604             		bne	ZCHK2	;CHECK FOR 0
0d20 : a700             		staa	0,x	;RESET SIGN BITS
0d22 : c604             		ldab	#4
0d24 : a600             ZCHK2		lda	0,x	;GET MS BYTE
0d26 : 46               		rora		;MOVE A SIGN BIT TO N
0d27 : 8408             		anda	#8	;MASK N BIT
0d29 : 1b               		aba		;MERGE Z AND N
0d2a : 9a30             		ora	OVFLBF	;ADD IN V
0d2c : 06               		tap		; SET CCR
0d2d : 39               		rts
                        *
                        *
                        **
0d2e : bd03bc           SKYCLS		jsr	SKIPSP
0d31 : 2002             		bra	CLASS1
                        *
                        *
                        **CLASS
                        *CLASSIFY A CHARACTER IN THE A ACCUMULATOR
                        *CLASSIFICATION RETURNED IN B
                        *  0 ERROR
                        *  1 TERMINATOR
                        *  2 LETTER
                        *  3 NUMBER
                        *  4 )
                        *  5 (
                        *  6 +
                        *  7 -
AS02 Assembler for M6802 [1.42].                                     Page   35
-------------------------------- MicroBas.asm --------------------------------

                        *  8 SGN
                        *  9 ABS
                        * 10 *
                        * 11 /
                        * 12 ~
0d33 : a600             CLASS		lda	0,x	;GET CHAR
0d35 : c601             CLASS1		ldab	#1	;SET UP
0d37 : 810d             		cmpa	#CR	;CHECK FOR CR
0d39 : 2717             		beq	CLAS25
0d3b : 5a               		decb
0d3c : 36               		psha		;SAVE CHAR
0d3d : 8028             CLAS2B		suba	#'('	;REMOVE BIAS
0d3f : 2b10             		bmi	CLASS2	;CHECK ILLEGAL
0d41 : 8118             		cmpa	#'@'-'('	;CHECK LIMIT
0d43 : 230e             		bls	CLASS3	;NOT LETTER
0d45 : 8132             		cmpa	#'Z'-'('	;CHECK FOR LETTER
0d47 : 2306             		bls	CLAS1B
0d49 : 8136             		cmpa	#'^'-'(' ;CHECK FOR ILLEGAL
0d4b : 2604             		bne	CLASS2
0d4d : c60a             		ldab	#10	;FIX UP
0d4f : cb02             CLAS1B		addb	#02
0d51 : 32               CLASS2		pula		;RESTORE CHARACTER
0d52 : 39               CLAS25		rts		;DONE
0d53 : df24             CLASS3		stx	XSAVE2	;SAVE X REG
0d55 : ce0d61           		ldx	#CLSTBL	;POINT TO TABLE
0d58 : b70d5c           		sta	CLSOFF+1	;SET BIAS
0d5b : e600             CLSOFF		ldab	0,x	;GET CLASSIFICATION
0d5d : de24             		ldx	XSAVE2	;RESTORE X REG,
0d5f : 20f0             		bra	CLASS2
0d61 : 05040a06010700.. CLSTBL		fcb	5,4,10,6,1,7,0,11,3,3,3,3
0d6d : 03030303030301.. 		fcb	3,3,3,3,3,3,1,1,1,1,1,8,9
                        *
                        *
                        * RANDOM GENERATOR
                        *
0d7a : c608             RANDOM		ldab	#8	;SET COUNTER
0d7c : ce0000           		ldx	#RNDM
0d7f : a603             RPT		lda	3,x	;GET M.S. BYTE OF RANDOM NO.
0d81 : 48               		asla		;SHIFT IT LEFT THREE:
0d82 : 48               		asla		;TIMES TO GET BIT 28
0d83 : 48               		asla		;IN LINE WITH BIT 31
0d84 : a803             		eora	3,x	;XOR A WITH RANDOM NO
0d86 : 48               		asla		;PUT BIT 28.XOR31 IN
0d87 : 48               		asla		;CARRY BY SHIFTING LEFT
0d88 : 6900             		rol	0,x	;ROTATE ALL FOUR BYTES OF
0d8a : 6901             		rol	1,x	;THE RANDOM NO, ROTATING
0d8c : 6902             		rol	2,x	;THE CARRY INTO THE LSB
0d8e : 6903             		rol	3,x	;THE MSB IS LOST
0d90 : 5a               		decb		;DECREMENT THE COUNTER
0d91 : 26ec             		bne	RPT	;IF ITS NOT O, GO REPEAT
0d93 : a600             		lda	0,x	;PUT RANDOM # IN A
0d95 : 819f             		cmpa	#$9F	;CHECK IN RANGE
0d97 : 22e1             		bhi	RANDOM	;IN NOT GET ANOTHER
0d99 : 8b00             		adda	#0	;SET HALF CARRY
0d9b : 19               		daa
0d9c : 39               		rts
0d9d : 0000             ENDSTR		rmb	2
                        
                        		bss
1000 =                  		org	CODEBASE
1000 =                  STORSP		equ	*
                        
AS02 Assembler for M6802 [1.42].                                     Page   36
-------------------------------- MicroBas.asm --------------------------------

                        ;
                        ; If loading into RAM, this works, but it does not
                        ; work for ROM based versions of BASIC.
                        ;
                        	if	~FORCE_RTS
                        		code
1f00 =                  		org	EXTERN
1f00 : 39               		rts
                        	endif
                        		end
                        
                        
No errors in pass 2.
