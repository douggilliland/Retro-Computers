 1 REM*gomoku*
 2 S$=CHR$(12):Q$=CHR$(10)+CHR$(13)
 3 IH$=CHR$(30):RB$=CHR$(11)
 4 POKE11,233:POKE12,255:GOTO16
 5 FORJ=1TO3:S(J)=0:NEXT:FORJ=-2TO2
 6 P=B(Y+J*XX)+1:S(P)=S(P)+1:NEXT
 7 S2=S(2):S3=S(3):IFS2=0ORS3=0THEN9
 8 RETURN
 9 IFS2=5THENJ$=W$:GOTO74
 10 IFS3=4THENFF=1
 11 IFS2=0THENL=K(S3+1):GOTO13
 12 L=W(S2+1)
 13 FORJ=-2TO2:A=Y+J*XX:IFB(A)<>0THEN15
 14 Q(A)=Q(A)+L
 15 NEXT:RETURN
 16 J=0:XX=0:D=0:S2=0:S3=0:FF=0
 17 DIMB(100),Q(100),W(5),K(5):MV=1
 18 W(1)=4:W(2)=12:W(3)=30:W(4)=90:W(5)=10000:K(1)=4:K(2)=12
 19 K(3)=30:K(4)=100:K(5)=100000
 20 B1$="                                         "
 21 C$=S$:FORJ=1TO24:C$=C$+Q$:NEXT
 22 TH$="      Thinking                           "
 23 W$="   You win!  "
 24 MW$="   I win!  "
 25 E$="  ILLEGAL MOVE"
 26 F$="  Already used"
 27 YM$="   Your move "
 28 D$="    A Draw!"
 29 MM$="    My move  "
 30 PRINTIH$Q$Q$Q$SPC(6);
 31 PRINTQ$Q$Q$Q$"Do you want instructions?":GOSUB105
 32 IFA$<>"N"ANDA$<>"Y"THEN30
 33 IFA$="Y"THENGOSUB108
 34 PRINTIH$Q$Q$Q$Q$"Do you want the first move":GOSUB105
 35 M=1:IFA$="Y"THENM=0:GOTO37
 36 IFA$<>"N"THEN34
 37 GOSUB76:IFM=1THEN53
 38 J$=YM$:GOSUB126
 39 X=USR(X):R$=CHR$(PEEK(533))
 40 IFASC(R$)=13THEN39
 41 MG$=E$:R=VAL(R$):PRINTC$Q$LEFT$(LL$,22)R$
 42 IFASC(R$)<48ORASC(R$)>57THEN44
 43 GOTO45
 44 GOSUB130:GOTO38
 45 X=USR(X):CL$=CHR$(PEEK(533))
 46 IFASC(CL$)=13THEN45
 47 C=VAL(CL$):PRINTTAB(33)CL$
 48 IFASC(R$)<48ORASC(R$)>57THEN44
 49 PRINTS$LEFT$(LL$,36)R$","CL$
 50 R=R+1:C=C+1:FORJ=1TO100:Q(J)=0:NEXT:Q(55)=1:A=(R-1)*10+C
 51 IFB(A)<>0THENMG$=F$:GOTO44
 52 B(A)=1:GOSUB97
 53 J$=TH$:GOSUB129:IFMV=1THENMV=0:GOSUB134:GOTO66
 54 FORR=1TO10:H=(R-1)*10:FORC=1TO10:D=0:Y=H+C
 55 IFC<3ORC>8THEN57
 56 D=1:XX=1:GOSUB5
 57 IFR<3ORR>8THEN59
 58 D=D+1:XX=10:GOSUB5
 59 IFD<2THEN61
 60 XX=11:GOSUB5:XX=9:GOSUB5
 61 NEXT:NEXT:GOTO62
 62 XX=1:QQ=0:FORJ=1TO100:IFQQ>Q(J)THEN64
 63 XX=J:QQ=Q(J)
 64 NEXT
 65 IFQQ<4THENJ$=D$:GOTO74
 66 B(XX)=2:R=INT((XX-1)/10)+1:C=XX-10*(R-1):R=R-1:C=C-1:J$=MM$:GOSUB12
 67 R$=MID$(STR$(R),2,1):CL$=MID$(STR$(C),2,1)
 68 PRINTC$LEFT$(LL$,22)R$;
 69 PRINTLEFT$(LL$,11)CL$
 70 PRINTS$LEFT$(LL$,36)R$","CL$
 71 R=R+1:C=C+1:GOSUB97
 72 IFFF=0THENGOSUB104:GOTO38
 73 J$=MW$
 74 GOSUB129:FORJ=1TO5000:NEXT:PRINTS$;:END
 75 END
 76 REM * DRAW BOARD *
 77 X=0
 78 PRINTIH$"     0  1  2  3  4  5  6  7  8  9  "
 79 PRINT"   "CHR$(165);:FORN=1TO30:PRINTCHR$(150);:NEXT:PRINTCHR$(167)
 80 FORN=0TO9:PRINTRB$RB$RB$RB$;:FORN1=0TO9
 81 PRINT"  "CHR$(143);:NEXT:PRINTCHR$(136)" ":PRINTRB$RB$RB$RB$;
 82 FORN1=0TO9:PRINTCHR$(128)CHR$(128)CHR$(208);:NEXT:PRINTCHR$(136)
 83 NEXT:X=-1:PRINTS$Q$:FORN=0TO99STEP10
 84 X=X+1
 85 PRINT"   "CHR$(143)
 86 PRINTXCHR$(143):NEXT
 87 PRINT"   "CHR$(166);:FORN=1TO30:PRINTCHR$(135);:NEXT:PRINTCHR$(168)
 88 LL$=RB$:FORN=1TO36:LL$=LL$+RB$:NEXT
 89 PRINTS$Q$Q$Q$LL$"G":PRINTQ$Q$LL$"O"
 90 PRINTQ$Q$LL$"M"
 91 PRINTQ$Q$LL$"O"
 92 PRINTQ$Q$LL$"K"
 93 PRINTQ$Q$LL$"U"
 94 PRINTQ$Q$Q$
 95 PRINTB1$:PRINTB1$S$:RETURN
 96 REM *UPDATE BOARD*
 97 PRINTS$Q$:FORN=1TO100STEP10
 98 PRINTRB$RB$RB$RB$RB$;
 99 FORN1=NTON+9
 100 IFB(N1)=0THENPRINTRB$RB$RB$;
 101 IFB(N1)=1THENPRINTCHR$(161)RB$RB$;
 102 IFB(N1)=2THENPRINTCHR$(187)RB$RB$;
 103 NEXT:PRINTQ$:NEXT:RETURN
 104 FORN=1TO1000:NEXT:RETURN
 105 PRINTQ$"(TYPE 'Y' FOR YES,'N' FOR NO)"
 106 X=USR(X):A$=CHR$(PEEK(533))
 107 RETURN
 108 REM *INSTRUCTIONS*
 109 PRINTIH$
 110 PRINTQ$SPC(5)"The object is to be the first to get"
 111 PRINT"five of your stones in a row -"
 112 PRINT"horizontally,vertically or diagonally."
 113 PRINTSPC(5)"You play the white stones ("CHR$(161)")"
 114 PRINTSPC(5)"The computer will play black ("CHR$(187)")"
 115 PRINT"Enter your move by typing the row and "
 116 PRINT"column when asked."
 117 PRINT:PRINT:PRINT"Press the space bar to start."
 118 X=USR(X):IFPEEK(533)<>32THEN118
 119 RETURN
 126 REM *MESSAGE BOARD*
 127 PRINTC$B1$:PRINTJ$;
 128 PRINTTAB(16)"ROW "RB$" "RB$" COLUMN "RB$" "S$:RETURN
 129 PRINTC$B1$:PRINTJ$:RETURN
 130 REM *MESSAGE*
 131 PRINTC$MG$S$:GOSUB104:RETURN
 132 X=USR(X):A$=CHR$(PEEK(533))
 133 PRINTASC(A$)
 134 XX=55:IFB(XX)=0THENRETURN
 135 XX=56:RETURN
