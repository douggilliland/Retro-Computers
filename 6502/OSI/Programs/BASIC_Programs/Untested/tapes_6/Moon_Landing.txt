

 9 DEF FNR(N)=INT(N*100+.5)/100
 10 GOSUB9000:PRINT"MOON LANDING!!!":PRINT"---------------":PRINT
 11 INPUT"INSTRUCTIONS";Q$:IFLEFT$(Q$,1)="Y"THEN9500
 20 INPUT"READY";Q$
 25 POKE(530),1:KEY=57088
 30 IFLEFT$(Q$,1)="N"THENPRINT"GET READY QUICKLY!!!":PRINT:PRINT
 40 FORI=1TO3000:NEXT
 50 DEF FNR(N)=INT(.5+N*100)/100
 100 HT=1000:V=50:FU=100:BR=0:T=0
 110 PR=53281:G=10/6:SC=53390
 120 F=98.56:FH=1:SR=0:HV=20:HD=0
 121 FH=67.642
 122 GOSUB 1000
 124 POKE(PR),193:POKE(PR+64),161:GOTO220
 126 HT=FNR(HT):V=FNR(V):T=FNR(T):FU=FNR(FU)
 130 HV=FNR(HV):A$=STR$(HT)+"      ":B$=STR$(V)+"       "
 140 C$=STR$(T)+"      ":D$=STR$(HV)+"       "
 150 E$=STR$(FU)+"       ":F$=STR$(BR)+"       "
 160 FORI=1TO8:POKE(SC+I),ASC(MID$(A$,I,1))
 170 POKE(SC+I+10),ASC(MID$(B$,I,1))
 180 POKE(SC+I+20),ASC(MID$(C$,I,1))
 190 POKE(SC+I+30),ASC(MID$(D$,I,1))
 200 POKE(SC+I+128),ASC(MID$(E$,I,1))
 210 POKE(SC+I+138),ASC(MID$(F$,I,1)):NEXT
 215 RETURN
 220 POKE(KEY),127:K=PEEK(KEY):N=0
 230 IFK=255 THEN N=7:POKE(KEY),191:K=PEEK(KEY)
 240 IF K=255 THEN 290
 250 FOR J=1 TO 8
 255 IF(KOR2^(J-1))=255 THEN BR=N+(9-J)
 260 NEXT J:IF BR=10 THEN BR=0
 290 IF FU<=0 OR BR>9 THEN BR=0
 300 FU=FU-BR*.1:GOTO 2000
 340 GOSUB9000:PRINT"              CRASH!!!!!!!!!!"
 341 PRINT:PRINT:PRINT"        YOU'RE DEAD!!!!!"
 350 PRINT:PRINT:PRINT:PRINT:PRINT:END
 360 GOSUB9000:PRINT"SOFT LANDING"
 370 PRINT:PRINT"VELOCITY:";V:PRINT"FUEL:"FU:PRINT"TIME:";T:END
 1000 :GOSUB9000
 1001 PRINT"   HEIGHT    VEL       TIME    HOR. VEL"
 1002 PRINT:PRINT:PRINT:PRINT"   FUEL  BURN RATE"
 1003 FORI=1TO9:PRINT:NEXT
 1005 PG=54220
 1010 FORI=0TO49
 1015 L=PEEK(PG+I-1):N=161
 1020 IF L>161 THEN 1050
 1040 IF RND(1)<.7 THEN N=178
 1045 GOTO 1080
 1050 IF L=178 THEN N=176:GOTO 1080
 1060 IF RND(1)<.9 THEN N=178
 1080 POKE(PG+I),N:NEXT:RETURN
 1100 REM LEFT
 1110 FORI=0TO48
 1120 P=PEEK(PG+I+1):POKE(PG+I),P:NEXT
 1130 N=161:L=PEEK(PG+48)
 1140 IF L>161 THEN 1160
 1150 IF RND(1)<.6 THEN N=178
 1160 IF L=178 THEN N=176:GOTO 1195
 1170 IF RND(1)<.8 THEN N=178
 1195 POKE(PG+49),N:RETURN
 1200 REM RIGHT
 1210 FORI=49 TO 1 STEP -1
 1220 P=PEEK(PG+I-1):POKE(PG+I),P:NEXT
 1230 N=161:L=PEEK(PG)
 1240 IF L>161 THEN 1260
 1250 IF RND(1)<.6 THEN N=176
 1260 IF L=176 THEN N=178:GOTO 1295
 1270 IF RND(1)<.8 THEN N=176
 1295 POKE(PG),N:RETURN
 2000 SR=0:POKE(KEY),254:K=PEEK(KEY)
 2010 IF K=252 THEN SR=+1
 2020 IF K=250 THEN SR=-1
 2030 IF BR>0 AND PEEK(PR+128)=32 THEN POKE(PR+128),29
 2040 IF SR=1 THEN POKE(PR+65),60
 2050 IF SR=-1 THEN POKE(PR+63),62
 2060 IF ABS(HD)<10 THEN 2100
 2070 IF HD>0 THEN GOSUB 1100
 2080 IF HD<0 THEN GOSUB 1200
 2090 HD=0
 2100 A=F*BR/(FU+50)-G:V=V-A*.1
 2105 HV=HV-SR*FH/(FU+50):HD=HD+HV*.1
 2110 OH=HT:HD=HD+HV*.1:HT=HT-V*.1:T=T+.1
 2120 GOSUB 126:IF PEEK(PR+128)=29 THEN POKE(PR+128),32
 2130 POKE(PR+65),32:POKE(PR+63),32:POKE PR,32
 2140 POKE(PR+64),32:PR=54113-INT(OH/76)*64
 2150 POKE(PR),193:POKE(PR+64),161
 2160 IF HT>0 THEN 220
 2165 V=SQR(V*V+HV*HV)
 2170 IF V>5 THEN 340
 2180 IF ABS(HV)<1 AND PEEK(PR+128)=161THEN360
 2190 PRINT:PRINT:PRINT:PRINT:PRINT:PRINT"   OOOOOPPPPPSSSSS!!!!!!!"
 2200 PRINT:PRINT:PRINT"          LANDER OVERTURNS!!!!!!"
 2210 GOTO 350
 9000 FORI=1TO16:PRINT:NEXT:RETURN
 9500 PRINT"THE OBJECT OF THIS GAME IS TO LAND YOUR"
 9510 PRINT"SPACESHIP ON A FLAT AREA OF THE MOON"
 9520 PRINT"TO CHANGE BURN RATE OF MAIN ENGINE"
 9530 PRINT"PRESS A NUMBER FROM 1 TO 9"
 9540 PRINT"TO FIRE SIDE ROCKETS, HOLD DOWN"
 9550 PRINT"APROPRIATE SHIFT KEY (LEFT OR RIGHT)"
 9560 PRINT"YOU MUST LAND WITH VELOCITY LESS THAN 5"
 9670 PRINT"AND HOTIZONTAL VELOCITY LESS THAN 1"
 9680 PRINT:PRINT"GOOD LUCK!":GOTO 20
OK
