

 10 DEFFNR(R)=INT(RND(456)*R)
 20 DIMD(10),K(15,4),A(5):GO$="LGSECPDTMJ"
 25 G$="":FORI=1TO56:G$=G$+CHR$(255):NEXTI
 30 GG=546:FORI=0TO216:POKE(GG+I),0:NEXTI
 40 INPUTR:R=RND(R):T=40:K=FNR(6)+15
 50 M=20:B=FNR(4)+2:F=1:FORI=1TOK
 60 GOSUB300:NEXTI:F=2:FORI=1TOB:GOSUB300:NEXT
 70 F=3:FORI=1TO100:GOSUB300:NEXTI
 80 GOSUB100:GOTO400
 100 E1=FNR(9):E2=FNR(6):E3=FNR(4):E4=FNR(4):RETURN
 300 F1=FNR(216)+GG:F2=FNR(4):P=PEEK(F1)
 310 IF(PANDINT(4^F2*3+.5))>0THEN300
 320 POKEF1,P+(F*4^F2):RETURN
 400 PRINTK;" KLINGONS":PRINTB;" STARBASES"
 420 B1=0:GOSUB500:GOTO600
 500 RESTORE:FORI=1TO10:READD(I):NEXTI:S=0:M=20:E=3000
 510 IFB1=0THENE=E-300:S=300:B1=1
 530 RETURN:DATA15,15,20,20,25,20,15,17,0,8
 600 GOSUB4000
 610 L=0:IFD(3)<0THEN700
 630 P1=E1:FORP2=E2*4TOE2*4+3:GOSUB9000
 640 FORI=1TO4:IFA(I)=1THENL=L+10
 650 IFA(I)=2THENL=L+1
 660 PRINT" "MID$(A$,I,1);:NEXTI:PRINT:NEXTP2:PP=E1+E2*9
 670 G$=LEFT$(G$,PP+1)+CHR$(L)+RIGHT$(G$,54-PP)
 700 FORI=0TO15:IFK(I,2)<=0THEN1050
 730 PRINT"KLINGON ATTACK!":F=INT(.5+250/K(I,3)):IFS<FTHEN780
 750 S=S-F:IFS=0THENPRINT"SHIELDS DOWN"
 770 PRINT"SHIELDS HELD":GOTO1050
 780 F=F-S:S=0:D1=FNR(10)+1
 790 IFD1=9THENPRINT"LIFE SUPPORT HIT":GOTO950
 800 IFD1=10THENPRINT"ENGINES HIT":GOTO1010
 900 PRINT"FUNCTION ";MID$(GO$,D1,1);" DAMAGED"
 920 D(D1)=D(D1)-F/5*RND(1):IFD(D1)<=0THENPRINT"NOW UNOPERATIONAL"
 940 GOTO980
 950 PRINT"REPAIR ENERGY:";F*RND(8):E=E-F*RND(0)
 980 IFE<=0THENPRINT"ENERGY EXHAUST":GOTO5000
 1000 GOTO1050
 1010 D2=F/100:PRINT"MANOUVRE RATE UP";D2*100"%":M=M*D2+M
 1050 NEXT
 1100 PRINT:INPUTF$:F=0:FORI=1TO10
 1140 IFMID$(GO$,I,1)=LEFT$(F$,1)THENF=I
 1150 NEXT
 1180 IFD(F)<0THENPRINT"DAMAGED":GOTO700
 1300 ONFGOTO1400,1315,610,1700,1800,2200,2410,2600,3000,2900
 1310 GOTO1100
 1315 PRINT"GALACTIC COORDS"E1","E2
 1320 FORI=0TO5:FORJ=0TO8:F=ASC(MID$(G$,I*9+J+2,1))
 1325 IFF=255THENPRINT" **";:GOTO1340
 1330 PRINTRIGHT$("  "+STR$(F),3);
 1340 NEXTJ:PRINT:NEXTI:GOTO1100
 1400 L=0
 1430 FORJ=E2-1TOE2+1:FORP1=E1-1TOE1+1:FORP2=J*4TOJ*4+3
 1440 IFP1<0ORP1>8ORP2<0ORP2>23THENL=9:GOTO1475
 1450 GOSUB9000:FORI=1TO4:IFA(I)=1THENL=L+10
 1465 IFA(I)=2THENL=L+1
 1470 NEXTI,P2
 1472 PP=P1+J*9+1:G$=LEFT$(G$,PP)+CHR$(L)+RIGHT$(G$,55-PP)
 1475 PRINTRIGHT$("   "+STR$(L),3);:L=0
 1480 NEXTP1:PRINT:NEXTJ:GOTO700
 1700 PRINT"ENERGY";E:PRINT"SHIELDS";S:E=E+S:INPUT"SET TO";S
 1710 IFS>ETHENPRINT"NOT ENOUGH":S=0:GOTO1700
 1720 IFS>500THENPRINT"TOO HIGH":S=0:GOTO1700
 1730 E=E-S:GOTO700
 1800 ONVAL(RIGHT$(F$,1))GOTO1910,1970,2020
 1820 GOTO1100
 1910 FORI=0TO15:IFK(I,2)<=0THEN1950
 1930 PRINT"DISTANCE";K(I,3):PRINT"DIRECTION";K(I,4)
 1950 NEXT:GOTO1100
 1970 PRINTT"STARDATES LEFT":PRINTK"KLINGONS":PRINTE+S"ENERGY"
 2000 PRINTD(10)"TORPEDOES":PRINTM"MANOUVRE RATE":GOTO1100
 2020 PRINT"ENERGY NEEDED IS";
 2030 L=0:N=0:FORI=0TO15:IFK(I,2)<=0THEN2070
 2050 IFK(I,3)>LTHENL=K(I,3)
 2060 N=N+1
 2070 NEXTI:K1=N*L*100+1:PRINTK1"UNITS":GOTO1100
 2200 N=0:FORI=0TO15:IFK(I,2)>0THENN=N+1
 2220 NEXT:IFN=0THENPRINT"NO TARGET":GOTO1100
 2270 PRINT"PHASERS LOCKED ON":INPUT"ENERGY TO USE";P1
 2290 IFP1>ETHENPRINT"NOT ENOUGH":GOTO1700
 2300 E=E-P1:FORI=0TO15:IFK(I,2)<=0THEN2400
 2320 H=P1/(N*K(I,3)):K(I,2)=K(I,2)-H
 2330 IFK(I,2)<=0THENGOSUB2401:GOTO2400
 2340 PRINTH"UNIT HIT ON KLINGON AT"K(I,0);K(I,1):GOTO2400
 2400 NEXT:GOTO700
 2401 PRINT"KLINGON DESTROYED":H=GG+(K(I,1)+E2*4)*9+E1
 2403 PP=PEEK(H):PP=PP-INT(.5+4^K(I,0))
 2405 POKE(H),PP:K=K-1:IFK=0THEN5100
 2407 RETURN
 2410 PRINT"FUNCTION CAPACITY:-"
 2430 FORI=1TO8:PRINTD(I);"FUNCTION: ";
 2435 PRINT" ";MID$(GO$,I,1):NEXT
 2440 PRINTD(10)"-TORPEDOES LEFT":GOTO1100
 2600 IFD(10)=0THENPRINT"NO TORPEDOES":GOTO1100
 2610 N=0:FORI=0TO15:IFK(I,2)>0THENN=N+1
 2650 NEXT:IFN=0THENPRINT"NOTHING TO FIRE AT":GOTO1100
 2670 INPUT"BEARING";G:D(10)=D(10)-1
 2680 FORI=0TO15:IFK(I,2)<=0THEN2820
 2710 IFABS(K(I,4)-G)>.001THEN2820
 2720 IFRND(11)<(6-K(I,3))/6THEN2770
 2730 K(I,2)=K(I,2)-RND(1)*100:IFK(I,2)<=0THEN2770
 2750 PRINT"KLINGON DAMAGED":GOTO700
 2770 K(I,2)=0:GOSUB2401:GOTO700
 2820 NEXT:PRINT"MISSED":GOTO700
 2900 E=E-200:IFE<0THEN3190
 2910 GOSUB100:GOTO600
 3000 INPUT"COURSE";C:IFC<0ORC>12THEN3000
 3110 INPUT"WARP SPEED";V:INPUT"TIME";T1
 3130 IFV<1ORV>8ORT1<1ORT1>5THEN3000
 3180 D1=V*T1:T=T-T1:E=E-D1*M
 3190 IFE<0THENPRINT"ENERGY EXHAUST":GOTO5000
 3200 IFT<0THENPRINT"TIME OUT":GOTO5000
 3250 C=C*.524:F1=E1*4+E3+INT(SIN(C)*D1+.5)
 3270 F2=E2*4+E4-INT(COS(C)*D1+.5)
 3300 IFF1<0ORF1>35ORF2<0ORF2>23THEN3530
 3340 G1=INT(F1/4):G2=INT(F2/4):G3=F1-G1*4:G4=F2-G2*4
 3350 P1=G1:P2=F2:GOSUB9000:IFA(G3+1)=0THEN3430
 3380 PRINT"MANOUVRE IMPOSSIBLE"
 3390 PRINT"GALAXY COORDS";G1;G2:PRINT"SECTOR COORDS";G3;G4
 3400 PRINT"OCCUPIED":GOTO3500
 3430 E1=G1:E2=G2:E3=G3:E4=G4:GOSUB4000
 3440 P1=G1:P2=F2:GOSUB9000:IFA(G3)=2ORA(G3+2)=2THEN3480
 3450 P2=F2-1:GOSUB9000:IFA(G3+1)=2THEN3480
 3455 P2=F2+1:GOSUB9000:IFA(G3+1)=2THEN3480
 3460 GOTO610
 3480 INPUT"DOCK";Q$:IFQ$="YES"THENT=T-4:GOSUB500:IFT<0THEN3200
 3490 GOTO610
 3500 E=E+D1*M:T=T+T1:GOTO700
 3530 PRINT"STAY IN GALAXY!!":GOTO3500
 4000 FORI=0TO15:FORJ=0TO4:K(I,J)=0:NEXTJ,I
 4010 K1=0:P1=E1:F2=E2*4+E4:FORP2=E2*4TOE2*4+3
 4050 GOSUB9000:FORJ=0TO3:IFA(J+1)<>1THEN4310
 4110 K(K1,0)=J:K(K1,1)=P2-E2*4:K(K1,2)=100
 4120 K(K1,3)=SQR((P2-F2)^2+(J-E3)^2)
 4125 IFP2=F2THENS4=0:GOTO4140
 4130 S4=ABS(ATN((J-E3)/(P2-F2)))*57.2958
 4135 IF(P2-F2)*(J-E3)>0THENS4=90-S4
 4140 IFF2<=P2ANDJ>E3THENS4=S4+90
 4150 IFF2<P2ANDJ<=E3THENS4=S4+180
 4160 IFF2>=P2ANDJ<E3THENS4=S4+270
 4170 S4=S4/30:K(K1,4)=S4:K1=K1+1
 4310 NEXTJ,P2:RETURN
 5000 END
 5100 PRINT"WELL DONE":PRINT"ALL KLINGONS DESTROYED":END
 9000 PP=PEEK(GG+P1+P2*9):A$="":FORII=0TO3
 9020 P=INT(.5+(PPAND4^II*3)/4^II):A(II+1)=P
 9025 IFE1=P1ANDII=E3ANDP2=E2*4+E4THENA$=A$+"E":GOTO9070
 9030 IFP=0THENA$=A$+CHR$(213)
 9040 IFP=1THENA$=A$+"K"
 9050 IFP=2THENA$=A$+"B"
 9060 IFP=3THENA$=A$+"*"
 9070 NEXTII:RETURN
OK
