

 10 PRINTCHR$(12):PRINT:PRINT" GRAPH PLOTTER"
 20 PRINT" -------------":PRINT
 30 GOSUB9000
 40 INPUT"EQUATION Y=";A$
 50 N=1000
 55 PP=53260:DD=13:NT=17
 60 IFPEEK(N)<>35ORPEEK(N+1)<>35THENN=N+1:GOTO60
 70 N=N+DD
 80 I=1:I1=1
 85 RESTORE:FORJ=1TONT:READB$,Z
 86 IFMID$(A$,I1,LEN(B$))<>B$THENNEXT:GOTO90
 87 POKEI+N,Z:I1=I1+LEN(B$)-1:GOTO95
 90 POKEI+N,ASC(MID$(A$,I1,1))
 91 IFI>48THEN97
 95 I=I+1:I1=I1+1:IFI1<=LEN(A$)THEN85
 97 FORJ=ITO50:POKEJ+N,32:NEXT
 100 REM##
 101 DEFFNX(X)=SIN(X)+X/2                                           
 110 INPUT"X-AXIS LIMITS FROM";X1
 120 INPUT"                TO";X2
 130 IFX2<=X1THENPRINT"FIRST VALUE IS LOWER LIMIT!":GOTO110
 140 PRINT"PUT Y-AXIS LIMITS BOTH ZERO FOR AUTO SCALE"
 150 INPUT"Y-AXIS LIMITS FROM";Y1
 160 INPUT"                TO";Y2
 162 DX=(X2-X1)/34
 165 IFY1>=Y2THENGOSUB3000
 170 DY=(Y2-Y1)/14
 180 PRINTCHR$(12);Y2;TAB(15)"Y=";A$
 182 Y3=Y1+DY*7:X3=X1+DX*17
 190 PRINT:PRINT:PRINT:PRINT:PRINT:PRINT:PRINTY3
 200 PRINT:PRINT:PRINT:PRINT:PRINT:PRINT:PRINTY1
 210 PRINTX1;TAB(17)X3;TAB(34);X2;
 220 IF(Y1-DX*2)*(Y2+DX*2)>0THEN255
 230 Y=0:FORX=X1-DX*2TOX2+DX*15STEPDX
 240 GOSUB2000:NEXT
 250 POKE(PP+YP*64+XP-5),88
 255 IF(X1-DX*2)*(X2+DX*16)>0THEN300
 260 XP=INT(-X1/DX+.5):FORY=Y1TOY2+DY/2STEPDY
 270 YP=INT((Y2-Y)/DY+1.5):P0=PP+YP*64+XP+2
 280 IFPEEK(P0)=32ORPEEK(P0)>99THENPOKEP0,149
 290 NEXT:POKEP0,89
 300 FORX=X1-DXTOX2+DX*14.5STEPDX
 310 Y=FNX(X):GOSUB2000:NEXT
 500 POKE11,0:POKE12,253:X=USR(0)
 510 FORI=1TO16:PRINT:NEXT
 520 INPUT"ANOTHER SCALE";Q$
 530 IFLEFT$(Q$,1)="Y"THEN110
 540 RUN
 999 GOTO999
 1000 DATA+,163,-,164,*,165,/,166,^,167,=,170
 1010 DATASGN,173,INT,174,ABS,175,SQR,179
 1020 DATARND,180,LOG,181,EXP,182,COS,183
 1030 DATASIN,184,TAN,185,ATN,186
 2000 REM PUT X,Y ON
 2010 YS=INT((Y2-Y)*8/DY+4.5)
 2020 YP=INT(YS/8):CH=135-YS+YP*8
 2030 IFYP<0ORYP>16THENRETURN
 2040 XP=INT((X-X1)/DX+2.5)
 2050 IFXP<0ORXP>49THENRETURN
 2055 PN=PP+YP*64+XP:P1=PEEK(PN)
 2060 IFP1<33ORP1>99THENPOKEPN,CH
 2070 RETURN
 3000 Y1=1E37:Y2=-1E37
 3010 FORX=X1TOX2+DX/2STEPDX
 3020 Y=FNX(X):IFY<Y1THENY1=Y
 3030 IFY>Y2THENY2=Y
 3040 NEXT
 3050 L1=LOG((Y2-Y1)/14)/LOG(10)
 3060 I1=SGN(L1)*INT(ABS(L1)+.5)
 3061 I2=1:IFI1=0THEN3070
 3062 I2=1:FORI=0TOABS(I1)
 3064 IFI1<0THENI2=I2/10
 3066 IFI1>0THENI2=I2*10
 3068 NEXT
 3070 Y1=INT(Y1/I2+.5)*I2
 3080 Y2=INT(Y2/I2+.5)*I2
 3090 RETURN
 9000 PRINT" THIS PROGRAM PLOTS A GRAPH OF ANY FUNCTION"
 9010 PRINT"IN THE FORM Y=(A BASIC FUNCTION OF X)"
 9020 PRINT"USING ANY SCALES."
 9030 PRINT" SIMPLY ENTER YOUR FUNCTION AND THE"
 9040 PRINT"LIMITS OF THE X-AXIS"
 9050 PRINT"THEN EITHER ENTER THE Y-AXIS LIMITS,"
 9060 PRINT"OR ENTER ZEROS TO HAVE THE Y-AXIS"
 9070 PRINT"SCALED AUTOMATICALLY.":PRINT:RETURN
OK
