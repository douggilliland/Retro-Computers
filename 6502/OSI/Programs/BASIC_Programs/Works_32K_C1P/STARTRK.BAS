 10 FORI=1TO16:PRINT:NEXT
 20 PRINT"  STAR TREK!":PRINT"  ----------"
 30 PRINT:PRINT:PRINT:PRINT
 100 GS=546
 110 DIMJ(5,4),D(10)
 120 GO$="LGSECPDTMJ":FORI=GSTOGS+216
 130 POKEI,0:NEXT:T=120:DEFFNR(R)=INT(RND(R)*R)
 150 K=FNR(20)+40:B=FNR(10)+10:FORI=1TOB:P=FNR(210)+GS
 170 POKE(P),(PEEK(P)+1):NEXT:FORI=1TOK
 190 P=FNR(210)+GS:IFPEEK(P)>50THEN190
 192 H1=FNR(14):H2=FNR(15)
 194 H5=FNR(14):H6=FNR(15)
 195 W1=FNR(14):W2=FNR(15)
 200 POKEP,PEEK(P)+10:NEXT:E1=FNR(14):E2=FNR(15):GOSUB6000
 220 E3=FNR(10):E4=FNR(10):IFMID$(SR$,E3*10+E4+2,1)<>CHR$(213)THEN220
 225 GOSUB6100:GOSUB7000
 230 PRINT"THERE ARE";K;"KLINGONS":PRINT"AND";B;"STARBASES"
 240 PRINT"YOU ARE IN SECTOR";E1;E2:PRINT"GOOD LUCK!":PRINT:B1=0
 250 GOSUB500:GOTO700
 500 RESTORE:FORI=1TO10:READD(I):NEXT:S=0:M=5:E=3000
 520 IFB1=0THENE=E-300:S=300
 530 B1=1:RETURN
 540 DATA 15,15,20,20,25,20,15,17,0,8
 600 GOSUB7000:IFD(3)<0THEN700
 610 P=GS+E1*15+E2:Q=PEEK(P):IFQ<100THENQ=Q+100
 620 POKE(P),Q:FORI=0TO9:FORJ=0TO9
 640 PRINTMID$(SR$,I*10+J+2,1);" ";:NEXT:PRINT:NEXT
 700 FORI=0TO5:IFK(I,2)<=0THEN1010
 720 PRINT"KLINGON ATTACK!":F=500/K(I,3):IFK(I,2)>50THENF=F*2
 725 IFS<FTHEN750
 730 S=S-F:IFS<1ANDS>=0THENPRINT"SHIELDS DOWN":GOTO1010
 740 PRINT"SHIELDS HELD":GOTO1010
 750 F=F-S:S=0:D1=FNR(10)+1:IFD1>8THENF=F*RND(1):GOTO800
 770 PRINT"FUNCTION ";MID$(GO$,D1,1);" DAMAGED"
 780 D(D1)=D(D1)-F/5*RND(1):IFD(D1)<0THENPRINT"NOW UNOPERATIONAL"
 790 GOTO980
 800 IFD1=10THENPRINT"MANOUVRE RATE UP"F"%":M=M*(1+F/100):GOTO1010
 810 PRINT"LIFE SUPPORT DAMAGED":PRINT"REPAIR ENERGY:";F:E=E-F
 980 IFE<0THEN2910
 1010 NEXT
 1100 PRINT:INPUT"FUNCTION";F$:IFLEFT$(F$,1)="R"THEN5000
 1110 F=0:FORI=1TO10:IFMID$(GO$,I,1)=LEFT$(F$,1)THENF=I
 1180 NEXT:IFF=0THEN1100
 1200 IFD(F)<0THENPRINT"FUNCTION ";F$;" DAMAGED":GOTO1100
 1300 ONFGOTO1500,1320,610,1700,1800,2100,2410,2600,3100,2900
 1320 PRINT"   0  1  2  3  4  5  6  7  8  9  10 11 12 13 14"
 1350 FORI=0TO13:PRINTRIGHT$(STR$(I),2);:FORJ=0TO14
 1360 N=PEEK(GS+I*15+J):IFN>99THENPRINTRIGHT$("  "+STR$(N-100),3);
 1370 IFN<100THENPRINT" **";
 1380 NEXT:PRINT
 1390 NEXT:PRINT"SECTOR COORDS"E2;E1,:INPUT"FUNCTION";F$:GOTO1110
 1500 FORI=-1TO1:FORJ=-1TO1
 1510 IFE1+I<0ORE1+I>13ORE2+J<0ORE2+J>14THENPRINT" 9 ";:GOTO1550
 1520 P=GS+(E1+I)*15+E2+J:N=PEEK(P):IFN>99THENN=N-100
 1530 PRINTRIGHT$(STR$(N),2);" ";:N=N+100:POKEP,N
 1550 NEXT:PRINT:NEXT:GOTO700
 1700 PRINT"ENERGY:";E:PRINT"SHIELDS:";S:E=E+S
 1720 INPUT"SET TO";S:IFS<0THEN1720
 1730 IFS>ETHENPRINT"NOT ENOUGH ENERGY":GOTO1720
 1740 IFS>500THENPRINT"TOO HIGH":GOTO1720
 1750 E=E-S:GOTO700
 1800 CN=VAL(MID$(F$,2,1)):ONCNGOTO1830,1900,2000
 1830 FORI=0TO5:IFK(I,2)<=0THEN1850
 1840 PRINT"DISTANCE:";K(I,3):PRINT"DIRECTION:";K(I,4)
 1850 NEXT:GOTO1100
 1900 PRINTT"STARDATES":PRINTK"KLINGONS":PRINTE+S"ENERGY"
 1910 PRINTD(10);"TORPEDOES":PRINTM"MANOUVRE RATE":GOTO1100
 2000 PRINT"ENERGY NEEDED:";:L=0:N=0:FORI=0TO5:IFK(I,2)<=0THEN2030
 2020 N=N+1:IFK(I,3)>LTHENL=K(I,3)
 2030 NEXT:PRINTINT(N*L*50+1);"UNITS":GOTO1100
 2100 N=0:FORI=0TO5:IFK(I,2)>0THENN=N+1
 2110 NEXT:IFN=0THENPRINT"NOTHING TO FIRE AT":GOTO1100
 2120 PRINT"PHASERS LOCKED ON":INPUT"ENERGY TO USE";P1
 2130 IFP1>ETHENPRINT"NOT ENOUGH ENERGY":GOTO1700
 2140 E=E-P1:FORI=0TO5:IFK(I,2)<=0THEN2400
 2150 H=P1/(N*K(I,3)):K(I,2)=K(I,2)-H
 2160 IFK(I,2)<=0THENGOSUB2500:GOTO2400
 2170 PRINTINT(H*2);"%HIT ON KLINGON AT";K(I,1);K(I,0):GOTO2400
 2400 NEXT:GOTO700
 2410 PRINT"FUNCTION CAPACITY:-":FORI=1TO8
 2420 PRINTMID$(GO$,I,1);":";D(I):NEXT:PRINTD(10);"TORPEDOES":GOTO1100
 2500 K=K-1:PRINT"KLINGON DESTROYED!":P=K(I,0)*10+K(I,1)+2
 2510 GOSUB9000:IFK>0THEN2520
 2512 PRINT:PRINT:PRINT"WELL DONE! YOU HAVE DESTROYED ALL"
 2514 PRINT"THE KLINGONS AND SAVED THE GALAXY!":PRINT:END
 2520 P=E1*15+E2+GS:POKE(P),(PEEK(P)-10):RETURN
 2600 N=0:FORI=0TO5:IFK(I,2)>0THENN=N+1
 2610 NEXT:IFN=0THEN RINT"NO TARGETS":GOTO1100
 2660 IFD(10)=0THENPRINT"NO TORPEDOES LEFT":GOTO1100
 2670 D(10)=D(10)-1:INPUT"BEARING";G
 2680 FORI=0TO5:IFK(I,2)<=0ORABS(K(I,4)-G)>.01THEN2800
 2700 IFRND(1)<(12-K(I,3))/12THEN2750
 2710 K(I,2)=K(I,2)-RND(1)*100:IFK(I,2)<=0THEN2750
 2720 PRINT"KLINGON DAMAGED":GOTO700
 2750 K(I,2)=-1:GOSUB2500:GOTO700
 2800 NEXT:PRINT"MISSED":GOTO700
 2900 E=E-200
 2910 IFE<0THENPRINT"ENERGY EXHAUSTED":GOTO6900
 2920 E1=FNR(14):E2=FNR(15):E3=FNR(10):E4=FNR(10)
 2930 GOSUB6000:IFMID$(SR$,E3*10+E4+2,1)<>CHR$(213)THEN2930
 2940 GOSUB6100:GOSUB7000:PRINT"JUMPED":GOTO700
 3100 INPUT"COURSE";C:INPUT"WARP SPEED(1-20)";W
 3110 INPUT"TIME(1-5)";TM:IFC<0ORC>12ORW<1ORW>20ORTM<1ORTM>5THEN3100
 3130 T=T-TM:D=TM*W:E=E-D*M:IFE<0THEN2910
 3132 IFT>0THEN3140
 3134 PRINT"THE KLINGONS HAVE JUST PERFECTED"
 3136 PRINT"THEIR WEAPON":GOTO6920
 3140 F1=E1*10+E3:F2=E2*10+E4:TH=C/1.90986
 3150 F1=F1-INT(.5+D*COS(TH)):F2=F2+INT(.5+D*SIN(TH))
 3160 IFF1<0ORF1>139ORF2<0ORF2>149THENPRINT"OUTSIDE GALAXY!":GOTO3400
 3170 G1=INT(F1/10):G3=F1-G1*10:G2=INT(F2/10):G4=F2-G2*10
 3180 IFE1=G1ANDE2=G2THEN3300
 3190 E1=G1:E2=G2:E3=G3:E4=G4:GOSUB6000
 3200 IFMID$(SR$,E3*10+E4+2,1)<>CHR$(213)THEN3190
 3210 GOSUB6100:IFMID$(SR$,P-1,1)="B"THEN3500
 3230 IFP<12THEN3250
 3240 IFMID$(SR$,P-10,1)="B"THEN3500
 3250 IFMID$(SR$,P+10,1)="B"ORMID$(SR$,P+1)="B"THEN3500
 3260 IFE1=H1ANDE2=H2ORE1=H5ANDE2=H6THEN4000
 3265 IFE1=W1ANDE2=W2THEN4200
 3270 IFT>10ORMS>0THEN600
 3280 PRINT"SPY REPORTS INDICATE THE KLINGONS ARE"
 3290 PRINT"CLOSE TO PERFECTING THEIR SECRET WEAPON!"
 3295 MS=1:GOTO600
 3300 IFMID$(SR$,G3*10+G4+2,1)=CHR$(213)THEN3330
 3310 PRINT"SENSORS INDICATE DESTINATION IS OCCUPIED"
 3320 PRINT"BY UNKNOWN OBJECT. MANOUVRE ABORTED":GOTO1100
 3330 P=E3*10+E4+2:GOSUB9000:E3=G3:E4=G4:GOTO3210
 3400 E=E+D*M:T=T+TM:GOTO3100
 3500 INPUT"DOCK WITH STARBASE";F$
 3501 IFLEFT$(F$,1)="N"THEN600
 3510 GOSUB500:T=T-5:IFT>0THEN600
 3520 GOTO3132
 4000 IFRND(1)<.3THEN4100
 4010 FORI=1TO3:D(I)=D(I)-FNR(20):NEXT
 4015 PRINT:PRINT"WARNING!!!!"
 4020 PRINT"ENTERPRISE IN SAME SECTOR AS BLACK HOLE!"
 4030 PRINT"ALL SCANNERS DAMAGED BY UNKNOWN RADIATION"
 4040 PRINT"EXTREME DANGER!!!":FORI=1TO2000:NEXT
 4050 GOTO600
 4100 PRINT:PRINT:PRINT"ENTERPRISE HITS BLACK HOLE!!!"
 4110 IFRND(1)>.8THENPRINT"TOTEL DESTRUCTION!!":END
 4120 PRINT"PASSES THROUGH SPACE WARP TO UNKNOWN DESTINATION"
 4130 PRINT:PRINT:FORI=1TO2000:NEXT:GOTO2920
 4200 PRINT:PRINT"ENTERPRISE PASSES THROUGH TIME WARP"
 4210 IFS>0THENS=0:PRINT"SHIELDS ABSORBED!"
 4220 WW=FNR(20)*FNR(20)*(RND(1)-.5)
 4230 T=T+INT(WW):W1=FNR(14):W1=FNR(15):IFT<=0THEN3132
 4240 GOTO600
 5000 INPUT"FUNCTION TO REPAIR";R$
 5010 F=0:FORI=1TO10:IFMID$(GO$,I,1)=LEFT$(R$,1)THENF=I
 5020 NEXT:IFF=0THEN5000
 5025 IFD(F)>=0THENPRINT"REPAIR NOT NEEDED":GOTO1100
 5030 IFE<-D(F)*10THENPRINT"INSUFFICIENT ENERGY":GOTO1100
 5040 IFT<-D(F)/2THENPRINT"NOT ENOUGH TIME":GOTO1100
 5050 T=T+D(F)/2:E=E+D(F)*10:D(F)=.01:GOTO700
 6000 NS=INT(21-2*SQR(ABS((E1-7)^2+(E2-7)^2+FNR(5))))
 6020 SR$="":FORI=1TO102:SR$=SR$+CHR$(213):NEXT
 6025 N=PEEK(GS+E1*15+E2):IFN>99THENN=N-100
 6030 N1=INT(N/10):C$="K":GOSUB6040
 6035 N1=N-N1*10+.5:C$="B":GOSUB6040
 6037 N1=NS:C$="*":GOSUB6040:RETURN
 6040 IFN1<1THENRETURN
 6050 FORI=1TON1
 6060 P=FNR(100)+2:IFMID$(SR$,P,1)<>CHR$(213)THEN6060
 6070 SR$=LEFT$(SR$,P-1)+C$+RIGHT$(SR$,102-P):NEXT:RETURN
 6100 P=E3*10+E4+2:SR$=LEFT$(SR$,P-1)+"E"+RIGHT$(SR$,102-P):RETURN
 6900 PRINT"YOU HAVE FAILED TO DESTROY ALL THE KLINGONS"
 6920 PRINT"THE KLINGONS NOW TAKE OVER THE GALAXY!":END
 7000 N=PEEK(GS+E1*15+E2):IFN>99THENN=N-100
 7020 K1=0:FORI=0TO9:FORJ=0TO9
 7030 IFMID$(SR$,I*10+J+2,1)<>"K"THEN7110
 7040 K(K1,0)=I:K(K1,1)=J:K(K1,2)=50
 7045 IFRND(1)<.1THENK(K1,2)=100
 7050 K(K1,3)=SQR((E3-I)^2+(E4-J)^2)
 7060 IFE3=ITHENS3=90:GOTO7080
 7070 S3=ATN((J-E4)/(E3-I))*57.2958
 7080 IFI>E3THENS3=S3+180
 7085 IFI=E3ANDE4>JTHENS3=S3+180
 7090 IFS3<0THENS3=S3+360
 7100 K(K1,4)=S3/30:K1=K1+1
 7110 NEXTJ,I:FORI=K1TO5:K(I,2)=0:NEXT:RETURN
 9000 SR$=LEFT$(SR$,P-1)+CHR$(213)+RIGHT$(SR$,102-P):RETURN
