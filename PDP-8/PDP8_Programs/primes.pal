mul=7405
div=7407
*0
	jmp	init

*10
index,	0

*20
tmp,	0
index2,	0

*200
init,
	jmp start
	cla
	tad	(hello)
	jms	prntln
	tad	(name)
	jms	prntln
	decimal
	tad	(123)
	jms	prntnum
	tad	(0)
	jms	prntnum
	tad	(4095)
	jms	prntnum
	tad	(7)
	jms	prntnum
	tad	(12)
	jms	prntnum
	octal
	tad	(hello)
	jms	prntln
	tad	(short)
	jms	prntsh
	tad	(nl)
	jms	prntln
	tad	(short2)
	jms	prntsh
	tad	(nl)
	jms	prntln
	jmp	start
short,	text	/-ABCD[\]^_ !"#<=>?/
short2,	text	/MY FIRST SHORT/
nl,	15; 12; 0
hello,	"H; "e; "l; "l; "o; ",; " ; "W; "o; "r; "l; "d; "!; 15; 12; 0
name,	"W; "h; "a; "t; " ; "i; "s; " ; "y; "o; "u; "r; " ; "n; "a; "m; "e; "?; 15; 12; 0
bgnstr,	"B; "E; "G; "I; "N; 15; 12; 0
endstr,	"E; "N; "D; 15; 12; 0
PAGE
col,	0
row,	0
colsav,	0
rowsav,	0
num,	0
multi,	0
count,	0
start,
	tad	(bgnstr)
	jms	prntln
	cla
	tad	(0001)
	dca	col
	tad	(primes-1)
	dca	index2
	tad	[7774]
	isz	index2
	nop
	dca I	index2
nxtld,
	cla
	tad	[7777]
	isz	index2
	nop
	dca I	index2
	tad	index2		/ Load the row index and
	/cia			/ subtract it from the
	cma
	tad	[1]
	tad	(primes+526-1)	/ address of last bit-field for primes
	sza
	jmp	nxtld

	tad	(0001)
	dca	col
	tad	(primes)
	dca	row
nxtnum,
	cla
	tad I	row
	and	col
	sna
	jmp	noprint

	/ This is prime
	/ print it and mark multiples as non-prime
	cla
	tad	num
	jms	prntnum

	/ Save current row/col
	cla
	tad	col
	dca	colsav
	tad	row
	dca	rowsav

	tad	num
	dca	multi
rstrtmu,
	tad	num
	/cia			/ two's complement AC
	cma
	tad	[1]
	dca	count

nxtcnt,
	/ Search through word space until multi wraps back to zero
	jms	incr
	isz	multi
	jmp	chkmlt
	jmp	final		/ Overflow back to zero while searching for multi
	/ Check if this number is a multiple
chkmlt,
	isz	count
	jmp	nxtcnt		/ Nope, skip to next number
	tad	col		/ Yes, mark it as non-prime
	cma
	and I	row
	dca I	row
	jmp	rstrtmu

final,
	/ Restore original row/col
	tad	colsav
	dca	col
	tad	rowsav
	dca	row

noprint,
	jms	incr
	isz	num
	jmp	nxtnum
	tad	(endstr)
	jms	prntln
	hlt

incr,
	0
	/ Load col into accumulator while leaving link clear
	cla cll
	tad	col
	ral
	snl
	jmp	incrcol
	ral
	isz	row
incrcol,
	dca	col
	jmp I	incr

PAGE
prntbuf, 0; 0; 0; 0; 15; 12; 0
prntidx, 0
prntnum,	0
	mql
	dca	prntidx		/ Clear index to zero
	div; 12
	tad	("0)
	dca	prntbuf+3
	mqa
	sza
	isz	prntidx
	cla
	div; 12
	tad	("0)
	dca	prntbuf+2
	mqa
	sza
	isz	prntidx
	cla
	div; 12
	tad	("0)
	dca	prntbuf+1
	mqa
	sza
	isz	prntidx
	tad	("0)
	dca	prntbuf+0
	tad	prntidx
	/cia
	cma
	tad	[1]
	tad	(prntbuf+3)
	jms	prntln
	jmp I	prntnum

prntln, 0
	tad	[-1]
	dca	index
pl1,
	tad I 	index	/ Load next character with auto-increment
	sna		/ Check for NUL
	jmp I	prntln	/ Return from subroutine
	and	(177)	/ Clear parity bit
	tls		/ Clear flag and send byte: TCF + TPC
	tsf		/ Wait for byte to be sent
	jmp	.-1
	cla
	jmp	pl1

prntsh,	0
	tad	[-1]
	dca	index
ps1,
	tad I 	index	/ Load next character with auto-increment
	dca	tmp
	tad	tmp
	bsw
	and	(0077)
	sna
	jmp I	prntsh
	jms	prntch
	tad	tmp
	and	(0077)
	sna
	jmp I	prntsh
	jms	prntch
	jmp	ps1

prntch,	0
	tad	(-40)	/ Ascii space is the separation point
	spa
	tad	(100)
	tad	(40)
	tls		/ Clear flag and send byte: TCF + TPC
	tsf		/ Wait for byte to be sent
	jmp	.-1
	cla
	jmp I	prntch

PAGE
primes,

/ Load the reset address and replace the RIM loop with a jmp to reset
*7777
reset,	init
*7776
	jmp I	reset
$
