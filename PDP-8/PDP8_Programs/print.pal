mul=7405
div=7407
*0
	jmp	init

*10
index,	0

*20
tmp,	0

short,	text	/-ABCD[\]^_ !"#<=>?/
short2,	text	/MY FIRST SHORT/
nl,	15; 12; 0
hello,	"H; "e; "l; "l; "o; ",; " ; "W; "o; "r; "l; "d; "!; 15; 12; 0
name,	"W; "h; "a; "t; " ; "i; "s; " ; "y; "o; "u; "r; " ; "n; "a; "m; "e; "?; 15; 12; 0

*200
init,
	cla
	tad	(hello)
	jms	prntln
	tad	(name)
	jms	prntln
	decimal
	tad	(123)
	jms	prntnum
	tad	(0)
	jms	prntnum
	tad	(4095)
	jms	prntnum
	tad	(7)
	jms	prntnum
	tad	(12)
	jms	prntnum
	octal
	tad	(hello)
	jms	prntln
	tad	(short)
	jms	prntsh
	tad	(nl)
	jms	prntln
	tad	(short2)
	jms	prntsh
	tad	(nl)
	jms	prntln
	hlt

prntbuf, 0; 0; 0; 0; 15; 12; 0
prntidx, 0
prntnum,	0
	mql
	dca	prntidx		/ Clear index to zero
	div; 12
	tad	("0)
	dca	prntbuf+3
	mqa
	sza
	isz	prntidx
	cla
	div; 12
	tad	("0)
	dca	prntbuf+2
	mqa
	sza
	isz	prntidx
	cla
	div; 12
	tad	("0)
	dca	prntbuf+1
	mqa
	sza
	isz	prntidx
	tad	("0)
	dca	prntbuf+0
	tad	prntidx
	cia
	tad	(prntbuf+3)
	jms	prntln
	jmp I	prntnum

prntln, 0
	tad	[-1]
	dca	index
pl1,
	tad I 	index	/ Load next character with auto-increment
	sna		/ Check for NUL
	jmp I	prntln	/ Return from subroutine
	and	(177)	/ Clear parity bit
	tls		/ Clear flag and send byte: TCF + TPC
	tsf		/ Wait for byte to be sent
	jmp	.-1
	cla
	jmp	pl1

prntsh,	0
	tad	[-1]
	dca	index
ps1,
	tad I 	index	/ Load next character with auto-increment
	dca	tmp
	tad	tmp
	bsw
	and	(0077)
	sna
	jmp I	prntsh
	jms	prntch
	tad	tmp
	and	(0077)
	sna
	jmp I	prntsh
	jms	prntch
	jmp	ps1

prntch,	0
	tad	(-40)	/ Ascii space is the separation point
	spa
	tad	(100)
	tad	(40)
	tls		/ Clear flag and send byte: TCF + TPC
	tsf		/ Wait for byte to be sent
	jmp	.-1
	cla
	jmp I	prntch

// Load the reset address and replace the RIM loop with a jmp to reset
/*7777
/reset,	init
/*7776
/	jmp I	reset
$
