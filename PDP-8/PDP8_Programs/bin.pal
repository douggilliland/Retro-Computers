/ https://homepage.divms.uiowa.edu/~jones/pdp8/
/	PAPER TAPE BINARY AND RIM LOADERS FOR PAPER TAPE INPUT
/	BASED ON CODE PLACED IN PUBLIC DOMAIN BY C. J. LASNER
/	DERIVED BY CJL FROM DEC'S BIN LOADERS FOR USE IN P?S/8
/	MODIFIED FOR STAND-ALONE USE BY D. W. JONES

/	I/O INSTRUCTIONS USED.  COMMENT OUT ALL BUT ONE VERSION

/	TTY INPUT
/KSFIOT= KSF
/KRBIOT= KRB
/KCCIOT= KCC
/KRSIOT= KRS

/	HIGH SPEED READER INPUT
KSFIOT= RSF
KRBIOT= RFC RRB
KCCIOT= RFC
KRSIOT= RRB

* 7647	/HISTORICAL STARTING LOCATION MAY BE AS LOW AS 7625
	/RANGE 07600-07642 AVAILABLE FOR DISK BOOT ROUTINE

/	/	/	/	/	/	/	/
/       ROUTINE TO GET AND DECODE FRAME OF TAPE
/	SKIPS NEXT INSTRUCTION IF DATA OR ORIGIN WORD
/	RETURN DIRECTLY IF LEADER
/	PROCESS ORIGIN SETTING AND GET NEXT FRAME
/	ON RETURN, AC=0 ALWAYS
/	WHAT A KLUGE!

BEGG,	.-.			/HISTORICAL NAME
BEGG1,	JMS	BNREAD		/GET A FRAME
	TAD     BNHLT /(7402)   /TEST FOR ERROR (HIGH BITS)
	SPA SNA CLA             /SKIP IF (FRAME+7402) > 0
BNLINE, JMP     BGOOD           /JUMP IF NO
/				/
	HLT			/HERE IF ERROR
        JMP     BEGG1           /GET MORE CHARACTERS

BGOOD,  TAD     BCHAR           /\
        AND     BN300  /(300)	/ \
        TAD     BN7600 /(-200)	/  >BUMP RETURN IF DATA
	SPA                     / /             OR ORIGIN
        ISZ     BEGG            //
        SPA SNA CLA             /RETURN IF NOT FIELD SETTING
	JMP I   BEGG		/RETURN IF LEADER, SKIP RETURN
        TAD     BCHAR           /\           IF DATA OR ORIGIN
BN300,  AND     BFMASK /(70)	/ \
        TAD     BNCDF  /(CDF)	/  >PROCESS FIELD SETTINGS
        DCA     BMTEM           / /
        JMP     BEGG1           //

/	/	/	/	/	/	/	/
/       ROUTINE TO GET ONE FRAME TO AC AND BCHAR

BNREAD, .-.
        KSFIOT
        JMP     .-1             /WAIT FOR INPUT
        KRBIOT
        DCA     BCHAR           /SAVE FRAME
        TAD     BCHAR           /GET IT BACK
        JMP I   BNREAD          /RETURN

BFMASK, 70                      /HARMLESS AND
BNCDF,  CDF     00              /CDF CONSTANT

/	/	/	/	/	/	/	/
/       PROCESS END OF TAPE

BEND,   JMS     BASEMB          /GOT TRAILER, DO LAST WORD
        CIA                     /INVERT FOR CHECKSUM COMPARISON
BIPZP1, TAD     BCHKSM
BNHLT,  HLT                     /AC EQUALS ZERO IF NO ERRORS

/	/	/	/	/	/	/	/
/       MAIN PROGRAM

BBEGIN, KCCIOT			/START OF PROGRAM
BN7600, 7600			/GROUP 2 CLA USED AS CONSTANT
	RDF			/\
	TAD     BNCDF /(CDF)	/ >SETUP INITIAL LOADING FIELD
	DCA     BMTEM		//
BAUTO,  JMS     BEGG		/GET A FRAME
        JMP     .-1		/IGNORE LEADER
BEGO,   DCA     BCHKSM		/ZERO OR ADD TO CHECKSUM
	TAD     BMTEM		/SET UP
	DCA     BLODIT		/DESIRED FIELD
	TAD     BCHAR		/SAVE AS
	DCA     BWORD1		/FIRST FRAME
	JMS     BNREAD		/GET NEXT
	DCA     BWORD2		/SAVE AS SECOND FRAME
	JMS     BEGG		/GET NEXT FRAME
	JMP     BEND		/TRAILER, GO FINISH UP
	JMS     BASEMB		/
BLODIT, HLT			/CDF INTO LOADING FIELD
	SNL			/SKIP IF ORIGIN SETTING
	DCA I   RORG		/STORE THE WORD IF DATA
	ISZ     RORG		/PUSH POINTER
	SZL			/NOP NOT NEEDED; SKIP IF ORIGIN
	DCA     RORG		/SAVE ORIGIN
BCHEX,  TAD     BWORD1		/NOT RICE, CORN OR WHEAT
        TAD     BWORD2		/ADD LATEST WORDS
        TAD     BCHKSM		/AND CHECKSUM
        JMP     BEGO		/ACCUMULATE AND DO NEXT

/	/	/	/	/	/	/	/
/	ASSEMBLE A WORD FROM 2 FRAMES
/	AC = 0 ON ENTRY, AC = ASSEMBLED WORD ON EXIT
/	LINK = ORIGIN SETTING BIT ON EXIT

BASEMB, .-.
	TAD     BWORD1		/GET FIRST FRAME
	CLL RTL;RTL;RTL		/SHIFT LEFT 6
	TAD     BWORD2		/ADD ON SECOND FRAME
	JMP I   BASEMB		/RETURN

/	/	/	/	/	/	/	/

BCHAR,  .-.			/INPUT BUFFER
BMTEM,  .-.			/FIELD TEMPORARY
BCHKSM, .-.			/CHECKSUM
BWORD1, .-.                     /FRAME 1 TEMPORARY
BWORD2, .-.			/FRAME 2 TEMPORARY

/       THE RIM LOADER FOLLOWS IN 7756-7775

RORG=	7776			/USED BY RIM AND BIN FOR ORIGIN

* 7777	/ FINAL WORD OF CORE (AFTER RIM) JUMPS TO BIN
        JMP     BBEGIN          /START BINARY LOADER